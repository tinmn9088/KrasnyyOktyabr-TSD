// @strict-types
#Область ПрограммныйИнтерфейс

// Процедура добавляет запись о штрихкоде.
// 
// Параметры:
//  ИнвентаризацияСклад - ДокументСсылка.ИнвентаризацияСклад
//  Комплектовщик - СправочникСсылка.Комплектовщики
//  НомерЯчейки - Строка
//  Штрихкод - Строка
//  Время - Дата
Процедура ДобавитьШтрихкод(Знач ИнвентаризацияСклад, Знач Комплектовщик, Знач НомерЯчейки, Знач Штрихкод, Знач Время) Экспорт

    Запись = СоздатьМенеджерЗаписи();

    Запись.Документ = ИнвентаризацияСклад;
    Запись.Штрихкод = Штрихкод;
    Запись.Комплектовщик = Комплектовщик;
    Запись.НомерЯчейки = НомерЯчейки;
    Запись.Дата = Время;

    Запись.Записать();

КонецПроцедуры

// Процедура удаляет запись о штрихкоде.
// 
// Параметры:
//  ИнвентаризацияСклад - ДокументСсылка.ИнвентаризацияСклад
//  Штрихкод - Строка
Процедура УдалитьШтрихкод(Знач ИнвентаризацияСклад, Знач Штрихкод) Экспорт

    Запись = СоздатьМенеджерЗаписи();

    Запись.Документ = ИнвентаризацияСклад;
    Запись.Штрихкод = Штрихкод;

    Запись.Удалить();

КонецПроцедуры

// Процедура удаляет записи о штрихкодах комплектовщика.
// 
// Параметры:
//  ИнвентаризацияСклад - ДокументСсылка.ИнвентаризацияСклад - 
//  Комплектовщик - СправочникСсылка.Комплектовщики - 
// 
// Возвращаемое значение:
//  Число - Количество удаленных штрихкодов
Функция УдалитьШтрихкодыКомплектовщика(Знач ИнвентаризацияСклад, Знач Комплектовщик) Экспорт

    НачатьТранзакцию();

    Попытка

        КоличествоУдаленныхШтрихкодов = 0;
    
        // 1. Поиск всех штрихкодов комплектовщика
        Запрос = Новый Запрос("ВЫБРАТЬ
                              |    ВсеШтрихкоды.Штрихкод КАК Штрихкод
                              |ИЗ
                              |    РегистрСведений.ШтрихкодыИнвентаризацииОбуви КАК ВсеШтрихкоды
                              |ГДЕ
                              |    ВсеШтрихкоды.Документ = &Документ
                              |    И ВсеШтрихкоды.Комплектовщик = &Комплектовщик");

        Запрос.УстановитьПараметр("Комплектовщик", Комплектовщик);
        Запрос.УстановитьПараметр("Документ", ИнвентаризацияСклад);
        РезультатЗапроса = Запрос.Выполнить();
        Выборка = РезультатЗапроса.Выбрать();

        // 2. Удаление штрихкодов по одному
        Пока Выборка.Следующий() Цикл
            
            //@skip-check property-return-type
            Штрихкод = Выборка.Штрихкод; // Строка

            Запись = СоздатьМенеджерЗаписи();

            Запись.Документ = ИнвентаризацияСклад;
            Запись.Комплектовщик = Комплектовщик;
            Запись.Штрихкод = Штрихкод;

            Запись.Удалить();

            КоличествоУдаленныхШтрихкодов = КоличествоУдаленныхШтрихкодов + 1;

        КонецЦикла;

        ЗафиксироватьТранзакцию();

        Возврат КоличествоУдаленныхШтрихкодов;

    Исключение

        ОтменитьТранзакцию();
        ВызватьИсключение;

    КонецПопытки;

КонецФункции

#КонецОбласти