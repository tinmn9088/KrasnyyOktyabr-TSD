// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаСотрудник = Параметры.Сотрудник;

    ФормаСписокТоваров.Параметры.УстановитьЗначениеПараметра("Документ", Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    Элементы.СписокТоваров.Шапка = Ложь;

    Элементы.МагазинОтправитель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
    Элементы.СкладОтправитель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
    Элементы.Организация.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
    Элементы.МагазинПолучатель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
    Элементы.СкладПолучатель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
    Элементы.ОрганизацияПолучатель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;

    Элементы.СписокТоваровНоменклатура.АвтоМаксимальнаяШирина = Ложь;
    Элементы.СписокТоваровНоменклатура.МаксимальнаяШирина = 16;
    Элементы.СписокТоваровКод.АвтоМаксимальнаяШирина = Ложь;
    Элементы.СписокТоваровКод.МаксимальнаяШирина = 16;

#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

    Если ВводДоступен() И ИмяСобытия = ОповещенияФормыКлиент.ИмяСобытияСканированияШтрихкода() Тогда

        Штрихкод = Строка(Параметр);

        ОбработатьШтрихкод(Штрихкод);

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШтрихкодИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)

    Элементы.КнопкаОбработатьШтрихкод.Доступность = ЗначениеЗаполнено(Текст);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокТоваров

// Процедура обрабатывает выбор строки в списке товаров.
// 
// Параметры:
//  Элемент - ТаблицаФормы
//  ВыбраннаяСтрока - Число - Номер строки табличной части Товары
//  Поле - ПолеФормы
//  СтандартнаяОбработка - Булево
&НаКлиенте
Асинх Процедура СписокТоваровВыборАсинх(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

    Товар = Объект.Товары.Получить(ВыбраннаяСтрока - 1);
    ПредыдущееКоличество = Товар.Количество;

    НовоеКоличество = Ждать ВвестиЧислоАсинх(ПредыдущееКоличество, Строка(Товар.Номенклатура), , 3);

    Если НовоеКоличество <> Неопределено И НовоеКоличество > 0 И НовоеКоличество <> ПредыдущееКоличество Тогда

        ОбновитьКоличествоТовара(Объект.Ссылка, ВыбраннаяСтрока, НовоеКоличество);

        ОбновитьФорму();

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработатьШтрихкодСФормы(Команда)

    ОбработатьШтрихкод(ФормаШтрихкод);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПеремещенияТоваров

// Процедура обрабатывает штрихкод.
// 
// Параметры:
//  Штрихкод - Строка
&НаКлиенте
Процедура ОбработатьШтрихкод(Штрихкод)

    ПоказатьОповещениеОбУспешномСканировании("Штрихкод обработан", Штрихкод);

КонецПроцедуры

// Процедура обновляет количество товара.
// 
// Параметры:
//  ПеремещениеТоваров - ДокументСсылка.ПеремещениеТоваров
//  НомерТовара - Число
//  НовоеКоличествоТовара - Число
&НаСервереБезКонтекста
Процедура ОбновитьКоличествоТовара(Знач ПеремещениеТоваров, Знач НомерТовара, Знач НовоеКоличествоТовара)

    ПеремещениеТоваров.ПолучитьОбъект().ОбновитьКоличествоТовара(НомерТовара, НовоеКоличествоТовара);

КонецПроцедуры

#КонецОбласти

#Область ОповещенияПользователя

// Функция возвращает ключ уникальности оповещений пользователя.
// 
// Возвращаемое значение:
//  Строка
&НаКлиенте
Функция КлючУникальностиОповещенийПользователя()

    Возврат ИмяФормы;

КонецФункции

// Процедура воспроизводит звуковой сигнал и показывает оповещение.
// 
// Параметры:
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьОповещениеОбУспешномСканировании(Знач Заголовок, Знач Пояснение)

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеОбУспешномСканированииШтрихкода();

#КонецЕсли

    ОповещенияПользователяКлиент.ПоказатьОповещение(Заголовок, Пояснение, КлючУникальностиОповещенийПользователя());

КонецПроцедуры

#КонецОбласти

#Область Форма

// Процедура обновляет список товаров.
&НаКлиенте
Процедура ОбновитьФорму()

    Элементы.СписокТоваров.Обновить();

КонецПроцедуры

#КонецОбласти

#КонецОбласти