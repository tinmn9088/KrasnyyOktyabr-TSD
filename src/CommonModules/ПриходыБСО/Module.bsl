// @strict-types
#Область ПрограммныйИнтерфейс

// Функция определяет существует ли номенклатура с данным штрихкодом и либо добавляет товар
// в поступление товаров, либо увеличивает счетчик количества.
// 
// Параметры:
//  ПоступлениеТоваров - ДокументСсылка.ПоступлениеТоваров
//  ШтрихкодТовара - Строка
// 
// Возвращаемое значение:
//  См. НовоеОписаниеОбработкиШтрихкода
Функция ОбработатьШтрихкодТовара(Знач ПоступлениеТоваров, Знач ШтрихкодТовара) Экспорт
    
    // 1. Удаление специальных символов кода маркировки, которые могут поломать регистр сведений
    ШтрихкодыКлиентСервер.Сериализовать(ШтрихкодТовара);

    НачатьТранзакцию();

    Попытка

        ОписаниеОбработкиШтрихкода = НовоеОписаниеОбработкиШтрихкода();
        
        // 2. Поиск номенклатуры по штрихкоду 
        Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                              |    ВсеШтрихкодыНоменклатуры.Ссылка КАК Номенклатура
                              |ИЗ
                              |    Справочник.НоменклатураРозница.Штрихкоды КАК ВсеШтрихкодыНоменклатуры
                              |ГДЕ
                              |    ВсеШтрихкодыНоменклатуры.Штрихкод = &Штрихкод");

        Запрос.УстановитьПараметр("Штрихкод", ШтрихкодТовара);
        РезультатЗапроса = Запрос.Выполнить();
        Выборка = РезультатЗапроса.Выбрать();

        Если Не Выборка.Следующий() Тогда

            ОтменитьТранзакцию();

            ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаНеНайденаНоменклатура();

            Возврат ОписаниеОбработкиШтрихкода;

        КонецЕсли;

        //@skip-check property-return-type
        НоменклатураТовара = Выборка.Номенклатура; // СправочникСсылка.НоменклатураРозница
        
        // 3. Увеличение количества товара или добавление нового товара
        Если ШтрихкодЕстьВПоступлении(ПоступлениеТоваров, ШтрихкодТовара) Тогда

            НовоеКоличество = УвеличитьКоличествоТовара(ПоступлениеТоваров, ШтрихкодТовара, НоменклатураТовара);
            ОписаниеОбработкиШтрихкода.НовоеКоличество = НовоеКоличество;
            ОписаниеОбработкиШтрихкода.ПредыдущееКоличество = НовоеКоличество - 1;

        Иначе

            ДобавитьТоварВПоступление(ПоступлениеТоваров, ШтрихкодТовара, НоменклатураТовара);
            ОписаниеОбработкиШтрихкода.НовоеКоличество = 1;
            ОписаниеОбработкиШтрихкода.ПредыдущееКоличество = 0;

        КонецЕсли;

        ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаУспех();

        ЗафиксироватьТранзакцию();

        Возврат ОписаниеОбработкиШтрихкода;

    Исключение

        ОтменитьТранзакцию();
        ВызватьИсключение;

    КонецПопытки;

КонецФункции

// Функция добавляет штрихкод без номенклатуры в поступление товаров.
// 
// Параметры:
//  ПоступлениеТоваров - ДокументСсылка.ПоступлениеТоваров
//  ШтрихкодБезНоменклатуры - Строка
// 
// Возвращаемое значение:
//  См. НовоеОписаниеОбработкиШтрихкода
Функция ОбработатьШтрихкодБезНоменклатуры(Знач ПоступлениеТоваров, Знач ШтрихкодБезНоменклатуры) Экспорт

    ОписаниеОбработкиШтрихкода = НовоеОписаниеОбработкиШтрихкода();

    РегистрыСведений.ШтрихкодыТоваров.ДобавитьШтрихкод(ПоступлениеТоваров, ШтрихкодБезНоменклатуры);

    ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаУспех();

    Возврат ОписаниеОбработкиШтрихкода;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция создает описание обработки штрихкода.
// 
// Возвращаемое значение:
//  Структура - Описание обработки штрихкода:
// * КодВозврата - Число - Код, указывающий на результат обработки штрихкода
// * ПредыдущееКоличество - Неопределено, Число - Значение реквизита "Количество" до обработки штрихкода
// * НовоеКоличество - Неопределено, Число - Значение реквизита "Количество" после обработки штрихкода
Функция НовоеОписаниеОбработкиШтрихкода() Экспорт

    ОписаниеОбработкиШтрихкода = Новый Структура;

    ОписаниеОбработкиШтрихкода.Вставить("КодВозврата", КодВозвратаУспех());
    ОписаниеОбработкиШтрихкода.Вставить("ПредыдущееКоличество", Неопределено);
    ОписаниеОбработкиШтрихкода.Вставить("НовоеКоличество", Неопределено);

    Возврат ОписаниеОбработкиШтрихкода;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. РегистрСведенийМенеджер.ШтрихкодыТоваров.ЕстьШтрихкод
Функция ШтрихкодЕстьВПоступлении(Знач Документ, Знач Штрихкод)

    Возврат РегистрыСведений.ШтрихкодыТоваров.ЕстьШтрихкод(Документ, Штрихкод);

КонецФункции

// См. РегистрСведенийМенеджер.ШтрихкодыТоваров.ДобавитьШтрихкод
Процедура ДобавитьТоварВПоступление(Знач Документ, Знач Штрихкод, Знач Номенклатура)

    РегистрыСведений.ШтрихкодыТоваров.ДобавитьШтрихкод(Документ, Штрихкод, Номенклатура);

КонецПроцедуры

// См. РегистрСведенийМенеджер.ШтрихкодыТоваров.УвеличитьКоличество
Функция УвеличитьКоличествоТовара(Знач Документ, Знач Штрихкод, Знач Номенклатура)

    Возврат РегистрыСведений.ШтрихкодыТоваров.УвеличитьКоличество(Документ, Штрихкод, Номенклатура);

КонецФункции

#КонецОбласти