// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаСотрудник = Параметры.Сотрудник;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

    // Отключение видимости шапки динамического списка, чтобы сэкономить место на экране 
    Элементы.СписокПеремещений.Шапка = Ложь; 
	
	// Ограничение максимальной ширины колонок
    ОграничитьШиринуКолонки(Элементы.МагазинОтправитель);
    ОграничитьШиринуКолонки(Элементы.СкладОтправитель);
    ОграничитьШиринуКолонки(Элементы.Организация);
    ОграничитьШиринуКолонки(Элементы.МагазинПолучатель);
    ОграничитьШиринуКолонки(Элементы.СкладПолучатель);
    ОграничитьШиринуКолонки(Элементы.ОрганизацияПолучатель);

#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПеремещений

// Процедура открывает форму для заполнения перемещения товаров.
// 
// Параметры:
//  Элемент - ТаблицаФормы
//  Значение - Произвольный
//  СтандартнаяОбработка - Булево
&НаКлиенте
Процедура СписокПеремещенийТоваровВыборЗначения(Элемент, Значение, СтандартнаяОбработка)

    ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("Ключ", Значение);
    ПараметрыФормы.Вставить("Сотрудник", ФормаСотрудник);

    ОткрытьФорму("Документ.ПеремещениеТоваров.Форма.ФормаЗаполнения", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

// Процедура устанавливает максимальную ширину колонки в значение 8.
//
// Параметры:
//  Поле - ПолеФормы 
//
&НаКлиенте
Процедура ОграничитьШиринуКолонки(Поле)

    Поле.АвтоМаксимальнаяШирина = Ложь;
    Поле.МаксимальнаяШирина = 8;

КонецПроцедуры

#КонецЕсли

#КонецОбласти