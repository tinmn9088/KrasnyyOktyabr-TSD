// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаСотрудник = Параметры.Сотрудник;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    Если Не ТекущийПользовательИмеетПравоВыполнятьПриходы() Тогда

        СделатьКнопкуНедоступной(Элементы.КнопкаВыбратьПриход);

    КонецЕсли;

    Если Не ТекущийПользовательИмеетПравоВыполнятьПеремещения() Тогда

        СделатьКнопкуНедоступной(Элементы.КнопкаВыбратьПеремещение);

    КонецЕсли;

    Если Не ТекущийПользовательИмеетПравоВыполнятьИнвентаризацию() Тогда
        
        СделатьКнопкуНедоступной(Элементы.КнопкаВыбратьИнвентаризацию);
        
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПроцессПриход(Команда)

#Если Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда

    Закрыть();

#КонецЕсли
   
    // Открытие формы выбора 
    ПараметрыФормы = Новый Структура("Сотрудник", ФормаСотрудник);
    ОткрытьФорму("Обработка.ВыполнениеПриходов.Форма.ФормаВыбораВидаПрихода", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроцессПеремещение(Команда)

#Если Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда

    Закрыть();

#КонецЕсли
   
    // Открытие формы выбора 
    ПараметрыФормы = Новый Структура("Сотрудник", ФормаСотрудник);
    ОткрытьФорму("Обработка.ВыполнениеПеремещений.Форма.ФормаВыбораПеремещения", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроцессИнвентаризацию(Команда)
    
#Если Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда

    Закрыть();

#КонецЕсли

    // Открытие формы выбора 
    ПараметрыФормы = Новый Структура("Сотрудник", ФормаСотрудник);
    ОткрытьФорму("Обработка.ВыполнениеИнвентаризации.Форма.ФормаВыбораИнвентаризацииТоваров", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ТекущийПользовательИмеетПравоВыполнятьПриходы()

    Возврат ПравоДоступа("Использование", Метаданные.Обработки.ВыполнениеПриходов);

КонецФункции

&НаСервере
Функция ТекущийПользовательИмеетПравоВыполнятьПеремещения()

    Возврат ПравоДоступа("Использование", Метаданные.Обработки.ВыполнениеПеремещений);

КонецФункции

&НаСервере
Функция ТекущийПользовательИмеетПравоВыполнятьИнвентаризацию()

    Возврат ПравоДоступа("Использование", Метаданные.Обработки.ВыполнениеИнвентаризации);

КонецФункции

// Процедура добавляет астериск в начале заголовка и отключает доступность кнопки.
// 
// Параметры:
//  Кнопка - КнопкаФормы
&НаКлиенте
Процедура СделатьКнопкуНедоступной(Кнопка)

    Кнопка.Заголовок = "* " + Кнопка.Заголовок;
    Кнопка.Доступность = Ложь;

КонецПроцедуры

#КонецОбласти