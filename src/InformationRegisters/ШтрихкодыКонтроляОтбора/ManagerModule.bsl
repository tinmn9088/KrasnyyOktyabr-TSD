// @strict-types
#Область ПрограммныйИнтерфейс

// Функция проверяет, что запись со штрихкодом есть в регистре сведений.
// 
// Параметры:
//  Штрихкод - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если запись со штрихкодом найдена
Функция ЕстьШтриход(Знач Штрихкод) Экспорт
    
    // Удаление специальных символов кода маркировки, которые могут поломать регистр сведений
    ШтрихкодыКлиентСервер.Сериализовать(Штрихкод);

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                          |    ИСТИНА КАК ЕстьШтрихкод
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыКонтроляОтбора КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Штрихкод = &Штрихкод");

    Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Возврат Выборка.Следующий();

КонецФункции

// Функция производит поиск по штрихкоду.
// 
// Параметры:
//  Штрихкод - Строка
// 
// Возвращаемое значение:
// Неопределено, Структура - Результат поиска:
// * Дата - Дата
// * Документ - ДокументСсылка.Задание
// * НомерЗаданияНаОтбор - Число
// * Комплектовщик - СправочникСсылка.Комплектовщики
Функция НайтиПоШтрихкоду(Знач Штрихкод) Экспорт
    
    // Удаление специальных символов кода маркировки, которые могут поломать регистр сведений
    ШтрихкодыКлиентСервер.Сериализовать(Штрихкод);

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                          |    ВсеШтрихкоды.Штрихкод,
                          |    ВсеШтрихкоды.Дата,
                          |    ВсеШтрихкоды.Документ,
                          |    ВсеШтрихкоды.НомерЗаданияНаОтбор,
                          |    ВсеШтрихкоды.Комплектовщик
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыКонтроляОтбора КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Штрихкод = &Штрихкод");

    Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Выборка.Следующий() Тогда

        //@skip-check property-return-type
        Дата = Выборка.Дата; // Дата
        //@skip-check property-return-type
        Документ = Выборка.Документ; // ДокументСсылка.Задание
        //@skip-check property-return-type
        НомерЗаданияНаОтбор = Выборка.НомерЗаданияНаОтбор; // Число
        //@skip-check property-return-type
        Комплектовщик = Выборка.Комплектовщик; // СправочникСсылка.Комплектовщики

        Результат = Новый Структура;

        Результат.Вставить("Дата", Дата);
        Результат.Вставить("Документ", Документ);
        Результат.Вставить("НомерЗаданияНаОтбор", НомерЗаданияНаОтбор);
        Результат.Вставить("Комплектовщик", Комплектовщик);

        Возврат Результат;

    Иначе

        Возврат Неопределено;

    КонецЕсли;

КонецФункции

// Процедура проверяет, что в регистре нет записи со штрихкодом
// и добавляет новую запись.
// 
// Параметры:
//  Штрихкод - Строка
//  Документ - ДокументСсылка.Задание
//  НомерЗаданияНаОтбор - Число
//  Количество - Число
//  Комплектовщик - СправочникСсылка.Комплектовщики
//
// Возвращаемое значение:
//  Булево - Истина, если запись со штрихкодом добавлена
Функция ДобавитьШтрихкод(Знач Штрихкод, Знач Документ, Знач НомерЗаданияНаОтбор, Знач Количество, Знач Комплектовщик) Экспорт

    // Удаление специальных символов кода маркировки, которые могут поломать регистр сведений
    ШтрихкодыКлиентСервер.Сериализовать(Штрихкод);

    Если ЕстьШтриход(Штрихкод) Тогда

        Возврат Ложь;

    КонецЕсли;

    Запись = СоздатьМенеджерЗаписи();

    Запись.Штрихкод = Штрихкод;
    Запись.Документ = Документ;
    Запись.НомерЗаданияНаОтбор = НомерЗаданияНаОтбор;
    Запись.Количество = Количество;
    Запись.Комплектовщик = Комплектовщик;

    Запись.Записать();

    Возврат Истина;

КонецФункции

#КонецОбласти