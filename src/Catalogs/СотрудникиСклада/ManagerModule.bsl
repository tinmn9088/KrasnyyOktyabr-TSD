// @strict-types
#Область ПрограммныйИнтерфейс

// Функция выполняет поиск сотрудника склада по его табельному номеру.
// 
// Параметры:
//  ТабельныйНомер - Строка
// 
// Возвращаемое значение:
//  СправочникСсылка.СотрудникиСклада - Сотрудник склада с нужным табельным номером или пустая ссылка
Функция НайтиПоТабельномуНомеру(ТабельныйНомер) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ
                          |    СотрудникиСклада.Ссылка КАК СотрудникСклада
                          |ИЗ
                          |    Справочник.СотрудникиСклада КАК СотрудникиСклада
                          |ГДЕ
                          |    СотрудникиСклада.ТабельныйНомер = &ТабельныйНомер
                          |    И НЕ СотрудникиСклада.ПометкаУдаления");

    Запрос.УстановитьПараметр("ТабельныйНомер", ТабельныйНомер);

    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        Возврат ПустаяСсылка();

    КонецЕсли;
    
    //@skip-check property-return-type
    СотрудникСклада = Выборка.СотрудникСклада; // СправочникСсылка.СотрудникиСклада

    Возврат СотрудникСклада;

КонецФункции

#КонецОбласти