// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаИдентификаторТовара = Параметры.ИдентификаторТовара;
    ФормаИсходноеЗначение = Параметры.ИсходноеЗначение;
    ФормаНовоеЗначение = ФормаИсходноеЗначение;
    ФормаВозможностьУдаления = Параметры.ВозможностьУдаления;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    Элементы.КнопкаВыбратьУдалениеТовара.Видимость = ФормаВозможностьУдаления;
    Элементы.НовоеКоличество.Подсказка = СтрШаблон("Сейчас: %1", Формат(ФормаИсходноеЗначение, "ЧЦ=15; ЧДЦ=3; ЧН=-"));

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ВыбратьУдалениеТовараАсинх(Команда)

    Ответ = Ждать ВопросАсинх("", РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.ОК, "Удалить товар?"); // КодВозвратаДиалога  

    Если Ответ = КодВозвратаДиалога.ОК Тогда

        ОповеститьОРезультате( , Истина);

    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьНовоеКоличество(Команда)

    ОповеститьОРезультате(ФормаНовоеЗначение);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура закрывает текущую форму и оповещает форму-владельца (она сможет
// получить значение в обработчике ОбработкаВыбора).
// 
// Параметры:
//  НовоеЗначение - Неопределено, Число - Новое значение
//  ВыбраноУдаление - Булево
&НаКлиенте
Процедура ОповеститьОРезультате(Знач НовоеЗначение, Знач ВыбраноУдаление = Ложь)

    Результат = ОповещенияФормыКлиент.ОписаниеРезультатаИзмененияКоличестваТовара();

    Результат.ИдентификаторТовара = ФормаИдентификаторТовара;
    Результат.ИсходноеЗначение = ФормаИсходноеЗначение;
    Результат.НовоеЗначение = НовоеЗначение;
    Результат.ВыбраноУдаление = ВыбраноУдаление;
    
    // После оповещения текущая форма закрывается (см. ЗакрыватьПриВыборе) 
    ОповеститьОВыборе(Результат);

КонецПроцедуры

#КонецОбласти