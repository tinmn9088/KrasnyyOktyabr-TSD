// @strict-types
#Область ПрограммныйИнтерфейс

// Процедура заполняет табличную часть штрихкодами и помечает документ на удаление.
// 
// Параметры:
//  Задание - ДокументСсылка.Задание
Процедура ЗаполнитьШтрихкодамиЗадания(Знач Задание) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
                          |    ВсеШтрихкоды.Штрихкод,
                          |    ВсеШтрихкоды.Комплектовщик
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыКонтроляОтбора КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ");

    Запрос.УстановитьПараметр("Документ", Задание);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    // Очистка табличной части
    ТабличнаяЧасть.Очистить();

    Пока Выборка.Следующий() Цикл
        
        //@skip-check property-return-type
        Штрихкод = Выборка.Штрихкод; // Строка
        
        //@skip-check property-return-type
        Комплектовщик = Выборка.Комплектовщик; // СправочникСсылка.Комплектовщики
        
        // Определение типа штрихкода
        Тип = ШтрихкодыОбувиКлиентСервер.ТипШтрихкода(Штрихкод);

        // Запись в журнал ошибок в зависимости от типа штрихкода
        Если Тип = Перечисления.ТипШтрихкода.НомерКороба Тогда

            ДобавитьНомерКороба(Штрихкод, Комплектовщик);

        ИначеЕсли Тип = Перечисления.ТипШтрихкода.КодМаркировки Тогда

            ДобавитьКодМаркировки(Штрихкод, Комплектовщик);

        КонецЕсли;

    КонецЦикла;

    Записать();
    
    УстановитьПометкуУдаления(Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет новую строку и заполняет реквизит "Короб".
// 
// Параметры:
//  НомерКороба - Строка
//  Комплектовщик - СправочникСсылка.Комплектовщики
Процедура ДобавитьНомерКороба(Знач НомерКороба, Знач Комплектовщик)

    НоваяСтрока = ТабличнаяЧасть.Добавить();
    НоваяСтрока.Короб = НомерКороба;
    НоваяСтрока.Комплектовщик = Комплектовщик;

КонецПроцедуры

// Процедура добавляет новую строку и заполняет реквизит "КМ31".
// 
// Параметры:
//  КодМаркировки - Строка
//  Комплектовщик - СправочникСсылка.Комплектовщики
Процедура ДобавитьКодМаркировки(Знач КодМаркировки, Знач Комплектовщик)

    НоваяСтрока = ТабличнаяЧасть.Добавить();
    НоваяСтрока.КМ31 = КодМаркировки;
    НоваяСтрока.Комплектовщик = Комплектовщик;

КонецПроцедуры

#КонецОбласти