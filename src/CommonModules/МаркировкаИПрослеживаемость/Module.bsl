// @strict-types
#Область ПрограммныйИнтерфейс

// Функция определяет содержит ли акт приемки товаров данный код маркировки, был ли
// он уже обработан, и увеличивает счетчик с количеством принятого товара.
// 
// Параметры:
//  АктПриемкиТоваров - ДокументСсылка.АктПриемкиТоваров
//  КодМаркировки - Строка
// 
// Возвращаемое значение:
//  См. НовоеОписаниеОбработкиШтрихкода
Функция ОбработатьКодМаркировки(Знач АктПриемкиТоваров, Знач КодМаркировки) Экспорт

    НачатьТранзакцию();

    Попытка

        ОписаниеОбработкиШтрихкода = НовоеОписаниеОбработкиШтрихкода();
        
        // 1. Поиск строки табличной части "Номенклатура", подходящей коду маркировки
        Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                              |    ВсеКодыМаркировки.НомерСтроки КАК НомерКодаМаркировки,
                              |    ВсеКодыМаркировки.КлючСвязи КАК КлючСвязи,
                              |    ВсеКодыМаркировки.Проверен КАК Проверен
                              |ПОМЕСТИТЬ ИскомыеКодыМаркировки
                              |ИЗ
                              |    Документ.АктПриемкиТоваров.КодыМаркировки КАК ВсеКодыМаркировки
                              |ГДЕ
                              |    ВсеКодыМаркировки.Ссылка = &Документ
                              |    И ВсеКодыМаркировки.КодМаркировки = &КодМаркировки
                              |;
                              |
                              |////////////////////////////////////////////////////////////////////////////////
                              |ВЫБРАТЬ ПЕРВЫЕ 1
                              |    ВсяНоменклатура.НомерСтроки КАК НомерНоменклатуры,
                              |    ВсяНоменклатура.КлючСвязи КАК КлючСвязи
                              |ПОМЕСТИТЬ ИскомаяНоменклатура
                              |ИЗ
                              |    Документ.АктПриемкиТоваров.Номенклатура КАК ВсяНоменклатура
                              |ГДЕ
                              |    ВсяНоменклатура.Ссылка = &Документ
                              |    И ВсяНоменклатура.КлючСвязи В
                              |        (ВЫБРАТЬ ПЕРВЫЕ 1
                              |            ИскомыеКодыМаркировки.КлючСвязи
                              |        ИЗ
                              |            ИскомыеКодыМаркировки)
                              |;
                              |
                              |////////////////////////////////////////////////////////////////////////////////
                              |ВЫБРАТЬ
                              |    ИскомаяНоменклатура.НомерНоменклатуры КАК НомерНоменклатуры,
                              |    ИскомыеКодыМаркировки.НомерКодаМаркировки,
                              |    ИскомыеКодыМаркировки.Проверен КАК Проверен
                              |ИЗ
                              |    ИскомыеКодыМаркировки КАК ИскомыеКодыМаркировки
                              |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИскомаяНоменклатура КАК ИскомаяНоменклатура
                              |        ПО (ИскомаяНоменклатура.КлючСвязи = ИскомыеКодыМаркировки.КлючСвязи)");

        Запрос.УстановитьПараметр("КодМаркировки", КодМаркировки);
        Запрос.УстановитьПараметр("Документ", АктПриемкиТоваров);
        РезультатЗапроса = Запрос.Выполнить();
        Выборка = РезультатЗапроса.Выбрать();

        Если Не Выборка.Следующий() Тогда

            ОтменитьТранзакцию();

            ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаНеНайденКодМаркировки();

            Возврат ОписаниеОбработкиШтрихкода;

        КонецЕсли;
        
        // 2. Проверка, что код маркировки еще не обрабатывался
        //@skip-check property-return-type
        КодМаркировкиУжеПроверен = Выборка.Проверен; // Булево

        Если КодМаркировкиУжеПроверен Тогда

            ОтменитьТранзакцию();

            ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаКодМаркировкиУжеПроверен();

            Возврат ОписаниеОбработкиШтрихкода;

        КонецЕсли;
        
        // 3. Увеличение счетчика количества принятого товара для найденной номенклатуры
        ОбъектДокумента = АктПриемкиТоваров.ПолучитьОбъект(); 
             
        //@skip-check property-return-type
        НомерНоменклатуры = Выборка.НомерНоменклатуры; // Число
        ОписаниеОбработкиШтрихкода.НомерНоменклатуры = НомерНоменклатуры;

        Номенклатура = ОбъектДокумента.Номенклатура.Получить(НомерНоменклатуры - 1);
        ОписаниеОбработкиШтрихкода.ПредыдущееКоличество = Номенклатура.КоличествоПринятогоТовара;

        Номенклатура.КоличествоПринятогоТовара = Номенклатура.КоличествоПринятогоТовара + 1;
        ОписаниеОбработкиШтрихкода.НовоеКоличество = Номенклатура.КоличествоПринятогоТовара;

        Если Номенклатура.КоличествоПринятогоТовара > Номенклатура.КоличествоПоставленногоТовара Тогда

            ОтменитьТранзакцию();

            ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаКоличествоПоставленногоМеньшеПринятого();

            Возврат ОписаниеОбработкиШтрихкода;

        КонецЕсли;
        
        //@skip-check property-return-type
        НомерКодаМаркировки = Выборка.НомерКодаМаркировки; // Число
        ЗаписьКодаМаркировки = ОбъектДокумента.КодыМаркировки.Получить(НомерКодаМаркировки - 1);
        ЗаписьКодаМаркировки.Проверен = Истина;

        ОбъектДокумента.Записать();

        ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаУспех();

        ЗафиксироватьТранзакцию();

        Возврат ОписаниеОбработкиШтрихкода;

    Исключение

        ОтменитьТранзакцию();
        ВызватьИсключение;

    КонецПопытки;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция создает описание обработки штрихкода.
// 
// Возвращаемое значение:
//  Структура - Описание обработки штрихкода:
// * КодВозврата - Число - Код, указывающий на результат обработки штрихкода
// * НомерНоменклатуры - Неопределено, Число - Номер строки табличной части "Номенклатура" документа "АктПриемкиТоваров",
//                                             которой соответствовал обрабатываемый штрихкод
// * ПредыдущееКоличество - Неопределено, Число - Значение реквизита "КоличествоПринятогоТовара" до обработки штрихкода
// * НовоеКоличество - Неопределено, Число - Значение реквизита "КоличествоПринятогоТовара" после обработки штрихкода
Функция НовоеОписаниеОбработкиШтрихкода() Экспорт

    ОписаниеОбработкиШтрихкода = Новый Структура;

    ОписаниеОбработкиШтрихкода.Вставить("КодВозврата", КодВозвратаУспех());
    ОписаниеОбработкиШтрихкода.Вставить("НомерНоменклатуры", Неопределено);
    ОписаниеОбработкиШтрихкода.Вставить("ПредыдущееКоличество", Неопределено);
    ОписаниеОбработкиШтрихкода.Вставить("НовоеКоличество", Неопределено);

    Возврат ОписаниеОбработкиШтрихкода;

КонецФункции

#КонецОбласти