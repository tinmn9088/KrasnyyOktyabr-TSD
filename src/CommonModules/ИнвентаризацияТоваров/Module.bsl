// @strict-types
#Область ПрограммныйИнтерфейс

// Функция определяет итоги пересчета.
// 
// Параметры:
//  ПересчетТоваров - ДокументСсылка.ПересчетТоваров
// 
// Возвращаемое значение:
//  См. НовоеОписаниеИтоговПересчета
Функция ИтогиПересчета(Знач ПересчетТоваров) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ
                          |    ВсеТовары.Номенклатура КАК Номенклатура,
                          |    СУММА(ВсеТовары.КоличествоУпаковок) КАК КоличествоУчет,
                          |    ВсеТовары.УдалитьУникальныйИдентификаторХарактеристики КАК УникальныйИдентификаторХарактеристики,
                          |    0 КАК КоличествоФакт
                          |ПОМЕСТИТЬ ПроверяемыеТовары
                          |ИЗ
                          |    Документ.ПересчетТоваров.Товары КАК ВсеТовары
                          |ГДЕ
                          |    ВсеТовары.Ссылка = &Документ
                          |СГРУППИРОВАТЬ ПО
                          |    ВсеТовары.Номенклатура,
                          |    ВсеТовары.УдалитьУникальныйИдентификаторХарактеристики
                          |
                          |ИНДЕКСИРОВАТЬ ПО
                          |    Номенклатура,
                          |    УникальныйИдентификаторХарактеристики
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |ВЫБРАТЬ
                          |    ВсеШтрихкоды.Номенклатура КАК Номенклатура,
                          |    0 КАК КоличествоУчет,
                          |    ВсеШтрихкоды.УникальныйИдентификаторХарактеристики КАК УникальныйИдентификаторХарактеристики,
                          |    СУММА(ВсеШтрихкоды.Количество) КАК КоличествоФакт
                          |ПОМЕСТИТЬ НеоприходованныеТовары
                          |ИЗ
                          |    РегистрСведений.УдалитьШтрихкодыТоваровСХарактеристиками КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |СГРУППИРОВАТЬ ПО
                          |    ВсеШтрихкоды.Номенклатура,
                          |    ВсеШтрихкоды.УникальныйИдентификаторХарактеристики
                          |
                          |ИНДЕКСИРОВАТЬ ПО
                          |    Номенклатура,
                          |    УникальныйИдентификаторХарактеристики
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |ВЫБРАТЬ
                          |    ТоварыВПересчете.Номенклатура КАК Номенклатура,
                          |    ТоварыВПересчете.КоличествоУчет КАК КоличествоУчет,
                          |    ТоварыВПересчете.КоличествоФакт КАК КоличествоФакт,
                          |    ТоварыВПересчете.УникальныйИдентификаторХарактеристики КАК УникальныйИдентификаторХарактеристики
                          |ПОМЕСТИТЬ ВсеТоварыВПересчете
                          |ИЗ
                          |    ПроверяемыеТовары КАК ТоварыВПересчете
                          |
                          |ОБЪЕДИНИТЬ ВСЕ
                          |
                          |ВЫБРАТЬ
                          |    НеоприходованныеТовары.Номенклатура,
                          |    НеоприходованныеТовары.КоличествоУчет,
                          |    НеоприходованныеТовары.КоличествоФакт,
                          |    НеоприходованныеТовары.УникальныйИдентификаторХарактеристики
                          |ИЗ
                          |    НеоприходованныеТовары КАК НеоприходованныеТовары
                          |
                          |ИНДЕКСИРОВАТЬ ПО
                          |    Номенклатура,
                          |    УникальныйИдентификаторХарактеристики
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |УНИЧТОЖИТЬ ПроверяемыеТовары
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |УНИЧТОЖИТЬ НеоприходованныеТовары
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |ВЫБРАТЬ
                          |    ВсеТоварыВПересчете.Номенклатура КАК Номенклатура,
                          |    ВсеТоварыВПересчете.УникальныйИдентификаторХарактеристики,
                          |    СУММА(ВсеТоварыВПересчете.КоличествоФакт) - СУММА(ВсеТоварыВПересчете.КоличествоУчет) КАК Отклонение
                          |ИЗ
                          |    ВсеТоварыВПересчете КАК ВсеТоварыВПересчете
                          |ГДЕ
                          |    ВсеТоварыВПересчете.Номенклатура <> ЗНАЧЕНИЕ(Справочник.НоменклатураРозница.ПустаяСсылка)
                          |СГРУППИРОВАТЬ ПО
                          |    ВсеТоварыВПересчете.Номенклатура,
                          |    ВсеТоварыВПересчете.УникальныйИдентификаторХарактеристики");

    Запрос.УстановитьПараметр("Документ", ПересчетТоваров);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    ОписаниеИтоговПересчета = НовоеОписаниеИтоговПересчета();

    Пока Выборка.Следующий() Цикл
        
        //@skip-check property-return-type
        Отклонение = Выборка.Отклонение; // Число

        Если Отклонение > 0 Тогда

            ОписаниеИтоговПересчета.Излишки = ОписаниеИтоговПересчета.Излишки + Отклонение;

        ИначеЕсли Отклонение < 0 Тогда

            ОписаниеИтоговПересчета.Недостатки = ОписаниеИтоговПересчета.Недостатки - Отклонение;

        КонецЕсли;

    КонецЦикла;

    Возврат ОписаниеИтоговПересчета;

КонецФункции

// Функция определяет существует ли номенклатура с данным штрихкодом и увеличивает счетчик количества.
// 
// Параметры:
//  ПересчетТоваров - ДокументСсылка.ПересчетТоваров
//  ШтрихкодТовара - Строка
//  ДополнительныеПараметры - См. НовыеДополнительныеПараметрыОбработкиШтрихкода
// 
// Возвращаемое значение:
//  См. НовоеОписаниеОбработкиШтрихкода
Функция ОбработатьШтрихкодТовара(Знач ПересчетТоваров, Знач ШтрихкодТовара, Знач ДополнительныеПараметры) Экспорт

    НачатьТранзакцию();

    Попытка

        ОписаниеОбработкиШтрихкода = НовоеОписаниеОбработкиШтрихкода();
        
        // 1. Поиск номенклатуры по штрихкоду 
        Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                              |    ВсеШтрихкодыНоменклатуры.Ссылка КАК Номенклатура
                              |ИЗ
                              |    Справочник.НоменклатураРозница.Штрихкоды КАК ВсеШтрихкодыНоменклатуры
                              |ГДЕ
                              |    ВсеШтрихкодыНоменклатуры.Штрихкод = &Штрихкод");

        Запрос.УстановитьПараметр("Штрихкод", ШтрихкодТовара);
        РезультатЗапроса = Запрос.Выполнить();
        Выборка = РезультатЗапроса.Выбрать();

        Если Не Выборка.Следующий() Тогда

            ОтменитьТранзакцию();

            ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаНовыйШтрихкод();

            Возврат ОписаниеОбработкиШтрихкода;

        КонецЕсли;

        //@skip-check property-return-type
        НоменклатураТовара = Выборка.Номенклатура; // СправочникСсылка.НоменклатураРозница
        
        // 2. Добавление нового товара или обновление количества
        Если ШтрихкодЕстьВПересчете(ПересчетТоваров,
                                    ШтрихкодТовара,
                                    ДополнительныеПараметры.УникальныйИдентификаторХарактеристики) Тогда

            УстановитьКоличествоТовара(ПересчетТоваров,
                                       ШтрихкодТовара,
                                       ДополнительныеПараметры.УникальныйИдентификаторХарактеристики,
                                       ДополнительныеПараметры.НовоеКоличество);
        Иначе

            ДополнительныеПараметрыДобавленияШтрихкода = РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.НовыеДополнительныеПараметрыДобавленияШтрихкода();

            ДополнительныеПараметрыДобавленияШтрихкода.НаименованиеХарактеристики = ДополнительныеПараметры.НаименованиеХарактеристики;
            ДополнительныеПараметрыДобавленияШтрихкода.Номенклатура = НоменклатураТовара;
            ДополнительныеПараметрыДобавленияШтрихкода.Количество = ДополнительныеПараметры.НовоеКоличество;

            ДобавитьТоварВПересчет(ПересчетТоваров,
                                   ШтрихкодТовара,
                                   ДополнительныеПараметры.УникальныйИдентификаторХарактеристики,
                                   ДополнительныеПараметрыДобавленияШтрихкода);

        КонецЕсли;

        НовоеКоличество = ДополнительныеПараметры.НовоеКоличество;
        ОписаниеОбработкиШтрихкода.НовоеКоличество = НовоеКоличество;
        ОписаниеОбработкиШтрихкода.ПредыдущееКоличество = НовоеКоличество - 1;

        ОписаниеОбработкиШтрихкода.Номенклатура = НоменклатураТовара;
        ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаТоварНайден();

        ЗафиксироватьТранзакцию();

        Возврат ОписаниеОбработкиШтрихкода;

    Исключение

        ОтменитьТранзакцию();
        ВызватьИсключение;

    КонецПопытки;

КонецФункции

// Функция добавляет штрихкод без номенклатуры в пересчет товаров.
// 
// Параметры:
//  ПересчетТоваров - ДокументСсылка.ПересчетТоваров
//  ШтрихкодБезНоменклатуры - Строка
// 
// Возвращаемое значение:
//  См. НовоеОписаниеОбработкиШтрихкода
Функция ОбработатьШтрихкодБезНоменклатуры(Знач ПересчетТоваров, Знач ШтрихкодБезНоменклатуры) Экспорт

    ОписаниеОбработкиШтрихкода = НовоеОписаниеОбработкиШтрихкода();

    ДополнительныеПараметрыДобавленияШтрихкода = РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.НовыеДополнительныеПараметрыДобавленияШтрихкода();

    РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.ДобавитьШтрихкод(ПересчетТоваров,
                                                                               ШтрихкодБезНоменклатуры,
                                                                               Неопределено,
                                                                               ДополнительныеПараметрыДобавленияШтрихкода);

    ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаУспех();

    Возврат ОписаниеОбработкиШтрихкода;

КонецФункции

// Функция удаляет все штрихкоды, которые были обработаны для документа.
// 
// Параметры:
//  ПересчетТоваров - ДокументСсылка.ПересчетТоваров
// 
// Возвращаемое значение:
//  Число - Количество штрихкодов, которое было удалено
Функция ОчиститьШтрихкоды(Знач ПересчетТоваров) Экспорт

    Возврат РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.УдалитьШтрихкодыДокумента(ПересчетТоваров);

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция создает описание обработки штрихкода.
// 
// Возвращаемое значение:
//  Структура - Описание обработки штрихкода:
// * КодВозврата - Число - Код, указывающий на результат обработки штрихкода
// * Номенклатура - Неопределено, СправочникСсылка.НоменклатураРозница - Номенклатура
// * ПредыдущееКоличество - Неопределено, Число - Значение реквизита "Количество" до обработки штрихкода
// * НовоеКоличество - Неопределено, Число - Значение реквизита "Количество" после обработки штрихкода
Функция НовоеОписаниеОбработкиШтрихкода() Экспорт

    ОписаниеОбработкиШтрихкода = Новый Структура;

    ОписаниеОбработкиШтрихкода.Вставить("КодВозврата", КодВозвратаУспех());
    ОписаниеОбработкиШтрихкода.Вставить("Номенклатура", Неопределено);
    ОписаниеОбработкиШтрихкода.Вставить("ПредыдущееКоличество", Неопределено);
    ОписаниеОбработкиШтрихкода.Вставить("НовоеКоличество", Неопределено);

    Возврат ОписаниеОбработкиШтрихкода;

КонецФункции

// Функция создает дополнительные параметры для обработки штрихкода.
// 
// Возвращаемое значение:
//  Структура:
//  * УникальныйИдентификаторХарактеристики - Неопределено, УникальныйИдентификатор - Уникальный идентификатор характеристики
//  * НаименованиеХарактеристики - Неопределено, Строка - Наименование характеристики
//  * НовоеКоличество - Число
Функция НовыеДополнительныеПараметрыОбработкиШтрихкода() Экспорт

    ДополнительныеПараметры = Новый Структура;

    ДополнительныеПараметры.Вставить("УникальныйИдентификаторХарактеристики", Неопределено);
    ДополнительныеПараметры.Вставить("НаименованиеХарактеристики", Неопределено);
    ДополнительныеПараметры.Вставить("НовоеКоличество", 0);

    Возврат ДополнительныеПараметры;

КонецФункции

// Функция создает описание итогов пересчета.
// 
// Возвращаемое значение:
//  Структура - Описание обработки штрихкода:
// * Излишки - Число
// * Недостатки - Число
Функция НовоеОписаниеИтоговПересчета() Экспорт

    Возврат Новый Структура("Излишки, Недостатки", 0, 0);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. РегистрСведенийМенеджер.УдалитьШтрихкодыТоваровСХарактеристиками.ЕстьШтрихкод
Функция ШтрихкодЕстьВПересчете(Знач Документ, Знач Штрихкод, Знач УникальныйИдентификаторХарактеристики)

    Возврат РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.ЕстьШтрихкод(Документ,
                                                                                   Штрихкод,
                                                                                   УникальныйИдентификаторХарактеристики);

КонецФункции

// Функция определяет суммарное количество товара в документе.
// 
// Параметры:
//  Документ - ДокументСсылка.ПересчетТоваров
//  Номенклатура - СправочникСсылка.НоменклатураРозница
// 
// Возвращаемое значение:
//  Число - Суммарное количество для всех штрихкодов номенклатуры
Функция КоличествоТовара(Знач Документ, Знач Номенклатура)

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                          |    СУММА(ВсеШтрихкоды.Количество) КАК Количество
                          |ИЗ
                          |    РегистрСведений.УдалитьШтрихкодыТоваровСХарактеристиками КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |    И ВсеШтрихкоды.Номенклатура = &Номенклатура");

    Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
    Запрос.УстановитьПараметр("Документ", Документ);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        ВызватьИсключение ("Не удалось получить обновленное количество товара");

    КонецЕсли;
    
    //@skip-check property-return-type
    Количество = Выборка.Количество; // Число

    Возврат Количество;

КонецФункции

// См. РегистрСведенийМенеджер.УдалитьШтрихкодыТоваровСХарактеристиками.ДобавитьШтрихкод
//
// Параметры:
//  Документ - ДокументСсылка.ПересчетТоваров
//  Штрихкод - Строка
//  УникальныйИдентификаторХарактеристики - Неопределено, УникальныйИдентификатор - Уникальный идентификатор характеристики номенклатуры
//  ДополнительныеПараметры - См. РегистрСведенийМенеджер.УдалитьШтрихкодыТоваровСХарактеристиками.НовыеДополнительныеПараметрыДобавленияШтрихкода
//
// Возвращаемое значение:
//  Число - Количество товара
Функция ДобавитьТоварВПересчет(Знач Документ,
                               Знач Штрихкод,
                               Знач УникальныйИдентификаторХарактеристики,
                               Знач ДополнительныеПараметры)

    РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.ДобавитьШтрихкод(Документ,
                                                                               Штрихкод,
                                                                               УникальныйИдентификаторХарактеристики,
                                                                               ДополнительныеПараметры);

    Возврат КоличествоТовара(Документ, ДополнительныеПараметры.Номенклатура);

КонецФункции

// См. РегистрСведенийМенеджер.УдалитьШтрихкодыТоваровСХарактеристиками.УстановитьКоличество
Функция УстановитьКоличествоТовара(Знач Документ,
                                   Знач Штрихкод,
                                   Знач УникальныйИдентификаторХарактеристики,
                                   Знач Количество)

    РегистрыСведений.УдалитьШтрихкодыТоваровСХарактеристиками.УстановитьКоличество(Документ,
                                                                                   Штрихкод,
                                                                                   УникальныйИдентификаторХарактеристики,
                                                                                   Количество);

    Возврат Количество;

КонецФункции

#КонецОбласти