// @strict-types
#Область ПрограммныйИнтерфейс

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

// Процедура воспроизводит звуковой сигнал с вибрацией.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
Процедура ВоспроизвестиЗвуковоеОповещениеДляКодаВозврата(Знач КодВозврата) Экспорт

    ЭтоКодВозвратаОшибки = Ложь;
    Звук = ЗвукПоКодуВозврата(КодВозврата, ЭтоКодВозвратаОшибки);
    Вибрация = ?(ЭтоКодВозвратаОшибки, ВибрацияОшибки(), Истина);

    СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение(Звук, Вибрация);

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал об ошибке с вибрацией.
Процедура ВоспроизвестиЗвуковоеОповещениеОшибки() Экспорт

    СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение("oshibka", ВибрацияОшибки());

КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Значение параметра для процедуры СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение.
// 
// Возвращаемое значение:
//  Массив из Число - Список продолжительностей пауз и вибрация в миллисекундах.
Функция ВибрацияОшибки()
    
    // "ER" -> ". .-." (Азбука Морзе) -> ".   . ... ." 
    Вибрация = Новый Массив(8);

    ДлительностьСигналаТочки = 125; // в миллисекундах

    Вибрация[0] = 0; // Пауза
    Вибрация[1] = ДлительностьСигналаТочки; // Вибрация
    Вибрация[2] = ДлительностьСигналаТочки * 3; // Пауза
    Вибрация[3] = ДлительностьСигналаТочки; // Вибрация
    Вибрация[4] = ДлительностьСигналаТочки; // Пауза
    Вибрация[5] = ДлительностьСигналаТочки * 3; // Вибрация
    Вибрация[6] = ДлительностьСигналаТочки; // Пауза
    Вибрация[7] = ДлительностьСигналаТочки; // Вибрация

    Возврат Вибрация;

КонецФункции

// Функция возвращает имя звука, соответствующего коду возврата.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
//  ЭтоКодВозвратаОшибки - Булево - Выходной
// 
// Возвращаемое значение:
//  Неопределено, Строка - Звук, соответствующий коду возврата, или Неопределено
Функция ЗвукПоКодуВозврата(Знач КодВозврата, ЭтоКодВозвратаОшибки)

    ЭтоКодВозвратаОшибки = Ложь;

    Если ЭтоКодВозвратаУспех(КодВозврата) Тогда

        Возврат "success";

    ИначеЕсли ЭтоКодВозвратаНеНайденоЗаданиеНаОтборПоШтрихкоду(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаСлишкомМногоВКоробе(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаОтсутствуетДокументНаВнутреннееПеремещение(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеПроведен(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "povtor";

    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеПроведенДругим(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "chuzhoj";

    ИначеЕсли ЭтоКодВозвратаОшибкаПроверкиПроведенияШтрихкода(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеОтсканирован(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "povtor";

    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеОтсканированДругим(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "chuzhoj";

    ИначеЕсли ЭтоКодВозвратаТоварНайден(КодВозврата) Тогда

        Возврат "tovar";

    ИначеЕсли ЭтоКодВозвратаНеНайденаНоменклатура(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаНовыйШтрихкод(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "novyj";

    ИначеЕсли ЭтоКодВозвратаНеНайденКодМаркировки(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаКодМаркировкиУжеПроверен(КодВозврата) Тогда

        ЭтоКодВозвратаОшибки = Истина;
        Возврат "povtor";

    Иначе

        Возврат Неопределено;

    КонецЕсли;

КонецФункции

#КонецОбласти