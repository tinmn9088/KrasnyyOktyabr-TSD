// @strict-types
#Область ПрограммныйИнтерфейс

// Функция определяет количество штрихкодов, обработанных данным комплектовщиком.
// 
// Параметры:
//  ИнвентаризацияСклад - ДокументСсылка.ИнвентаризацияСклад
//  Комплектовщик - СправочникСсылка.Комплектовщики
// 
// Возвращаемое значение:
//  Число
Функция КоличествоШтрихкодовКомплектовщика(Знач ИнвентаризацияСклад, Знач Комплектовщик) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ
                          |    КОЛИЧЕСТВО(*) КАК Количество
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыИнвентаризацииОбуви КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |    И ВсеШтрихкоды.Комплектовщик = &Комплектовщик");

    Запрос.УстановитьПараметр("Комплектовщик", Комплектовщик);
    Запрос.УстановитьПараметр("Документ", ИнвентаризацияСклад);

    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        Возврат 0;

    КонецЕсли;
    
    //@skip-check property-return-type
    КоличествоШтрихкодов = Выборка.Количество; // Число

    Возврат КоличествоШтрихкодов;

КонецФункции

// Функция определяет был ли уже отсканирован штрихкод номера короба, и если нет,
// то добавляет его в табличную часть "Штрихкоды".
// 
// Параметры:
//  ИнвентаризацияСклад - ДокументСсылка.ИнвентаризацияСклад
//  Штрихкод - Строка
//  Комплектовщик - СправочникСсылка.Комплектовщики
//  НомерЯчейки - Строка
// 
// Возвращаемое значение:
//  См. НовоеОписаниеОбработкиШтрихкода
Функция ОбработатьШтрихкодОбуви(Знач ИнвентаризацияСклад, Знач Штрихкод, Знач Комплектовщик, Знач НомерЯчейки) Экспорт

    НачатьТранзакцию();

    Попытка

        ОписаниеОбработкиШтрихкода = НовоеОписаниеОбработкиШтрихкода();
        
        // 1. Проверка, что штрихкод еще не был обработан
        Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                              |    ВсеШтрихкоды.Комплектовщик,
                              |    ВсеШтрихкоды.Дата
                              |ИЗ
                              |    РегистрСведений.ШтрихкодыИнвентаризацииОбуви КАК ВсеШтрихкоды
                              |ГДЕ
                              |    ВсеШтрихкоды.Документ = &Документ
                              |    И ВсеШтрихкоды.Штрихкод = &Штрихкод");

        Запрос.УстановитьПараметр("Документ", ИнвентаризацияСклад);
        Запрос.УстановитьПараметр("Штрихкод", Штрихкод);

        РезультатЗапроса = Запрос.Выполнить();
        Выборка = РезультатЗапроса.Выбрать();

        Если Выборка.Следующий() Тогда

            ОтменитьТранзакцию();

            //@skip-check property-return-type
            КомплектовщикОбработавшийШтрихкод = Выборка.Комплектовщик; // СправочникСсылка.Комплектовщики

            // Проверка, кто уже обработал штрихкод
            Если КомплектовщикОбработавшийШтрихкод = Комплектовщик Тогда
                
                //@skip-check property-return-type
                ДатаПредыдущегоСканирования = Выборка.Дата; // Дата

                ОписаниеОбработкиШтрихкода.ДатаПредыдущегоСканирования = ДатаПредыдущегоСканирования;
                ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаШтрихкодУжеОтсканирован();

            Иначе
                
                //@skip-check property-return-type
                ДатаПредыдущегоСканирования = Выборка.Дата; // Дата

                ОписаниеОбработкиШтрихкода.КомплектовщикПредыдущегоСканирования = КомплектовщикОбработавшийШтрихкод;
                ОписаниеОбработкиШтрихкода.ДатаПредыдущегоСканирования = ДатаПредыдущегоСканирования;
                ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаШтрихкодУжеОтсканированДругим();

            КонецЕсли;

            Возврат ОписаниеОбработкиШтрихкода;

        КонецЕсли;
  
        // 2. Добавление штрихкода в документ и регистр сведений
        РегистрыСведений.ШтрихкодыИнвентаризацииОбуви.ДобавитьШтрихкод(ИнвентаризацияСклад,
                                                                       Комплектовщик,
                                                                       НомерЯчейки,
                                                                       Штрихкод,
                                                                       ТекущаяДатаСеанса());

        ОписаниеОбработкиШтрихкода.КодВозврата = КодВозвратаУспех();

        ЗафиксироватьТранзакцию();

        Возврат ОписаниеОбработкиШтрихкода;

    Исключение

        ОтменитьТранзакцию();
        ВызватьИсключение;

    КонецПопытки;

КонецФункции

// Процедура удаляет штрихкод из регистра сведений.
// 
// Параметры:
//  ИнвентаризацияСклад - ДокументСсылка.ИнвентаризацияСклад
//  Штрихкод - Строка
Процедура УдалитьШтрихкод(Знач ИнвентаризацияСклад, Знач Штрихкод) Экспорт

    РегистрыСведений.ШтрихкодыИнвентаризацииОбуви.УдалитьШтрихкод(ИнвентаризацияСклад, Штрихкод);

КонецПроцедуры

// См. РегистрСведенийМенеджер.ШтрихкодыИнвентаризацииОбуви.УдалитьШтрихкодыКомплектовщика
Функция УдалитьШтрихкодыКомплектовщика(Знач ИнвентаризацияСклад, Знач Комплектовщик) Экспорт

    Возврат РегистрыСведений.ШтрихкодыИнвентаризацииОбуви.УдалитьШтрихкодыКомплектовщика(ИнвентаризацияСклад,
                                                                                         Комплектовщик);

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция создает описание обработки штрихкода.
// 
// Возвращаемое значение:
//  Структура - Описание обработки штрихкода:
// * КодВозврата - Число - Код, указывающий на результат обработки штрихкода
// * ДатаПредыдущегоСканирования - Неопределено, Дата - Дата предыдущего сканирования, если штрихкод уже был отсканирован
// * КомплектовщикПредыдущегоСканирования - Неопределено, СправочникСсылка.Комплектовщики - Комплектовщие предыдущего 
//                                                                                          сканирования, если штрихкод уже был
//                                                                                          отсканирован другим комплектовщиком
Функция НовоеОписаниеОбработкиШтрихкода() Экспорт

    ОписаниеОбработкиШтрихкода = Новый Структура;

    ОписаниеОбработкиШтрихкода.Вставить("КодВозврата", КодВозвратаУспех());
    ОписаниеОбработкиШтрихкода.Вставить("ДатаПредыдущегоСканирования", Неопределено);
    ОписаниеОбработкиШтрихкода.Вставить("КомплектовщикПредыдущегоСканирования", Неопределено);

    Возврат ОписаниеОбработкиШтрихкода;

КонецФункции

#КонецОбласти