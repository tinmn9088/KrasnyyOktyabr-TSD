#Область ПрограммныйИнтерфейс

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

// Процедура настраивает драйвер для сканера штрихкода Newland N7 Cachalot Pro и добавляет его в
// коллекцию ВнешниеКомпоненты.
// 
// Параметры:
//  ВнешниеКомпоненты - См. МенеджерВнешниеКомпонентыКлиент.НоваяКоллекцияВнешнихКомпонент
Процедура НастроитьДрайвер(ВнешниеКомпоненты) Экспорт

    Перем МестоположениеВнешнейКомпоненты;
    Перем Драйвер;

    УстановитьВнешнююКомпонентуСДрайвером(МестоположениеВнешнейКомпоненты);

    ПодключитьДрайвер(МестоположениеВнешнейКомпоненты, Драйвер);
    
    // Добавление драйвера в коллекцию ВнешниеКомпоненты
    ВнешниеКомпоненты.Вставить(КлючВнешнейКомпоненты(), Драйвер);

КонецПроцедуры

// Процедура посылает оповещение.
// 
// Параметры:
//  ОписаниеВнешнегоСобытия - См. МенеджерВнешниеКомпонентыКлиент.НовоеОписаниеВнешнегоСобытия
Процедура ОбработатьВнешнееСобытие(ОписаниеВнешнегоСобытия) Экспорт

    Событие = ОповещенияФормыКлиент.ИмяСобытияСканированияШтрихкода();
    Штрихкод = ОписаниеВнешнегоСобытия.Данные;
    Источник = "ПодключаемоеОборудование";

    // Отправить оповещение
    Оповестить(Событие, Штрихкод, Источник);

КонецПроцедуры

// Процедура отключает драйвер.
// 
// Параметры:
//  Драйвер - Произвольный
Процедура ОтключитьДрайвер(Драйвер) Экспорт

    Драйвер.Отключить(КлючВнешнейКомпоненты());

КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция возвращает местоположение внешней компоненты.
// 
// Возвращаемое значение:
//  Строка - Местоположение внешней компоненты
Функция МестоположениеВнешнейКомпонентыСДрайвером() Экспорт

    Возврат "ОбщийМакет.Драйвер1ССканерШтрихкода";

КонецФункции

// Функция возвращает ключ для коллекции внешних компонент.
// 
// Возвращаемое значение:
//  Строка - Ключ для коллекции внешних компонент
Функция КлючВнешнейКомпоненты() Экспорт

    Возврат "ScannerNewlandN7CachalotPro";

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

// Процедура устанавливает внешнюю компоненту с драйвером. Если при установке 
// возникла ошибка, процедура вызывает исключение.
// 
// Параметры:
//  МестоположениеВнешнейКомпоненты - Строка - Содержит местоположение внешней компоненты, откуда она была установлена
Процедура УстановитьВнешнююКомпонентуСДрайвером(МестоположениеВнешнейКомпоненты)

    МестоположениеВнешнейКомпоненты = МестоположениеВнешнейКомпонентыСДрайвером();

    Попытка
        
        //@skip-check bsl-legacy-check-static-feature-access
        УстановитьВнешнююКомпоненту(МестоположениеВнешнейКомпоненты);

    Исключение

        ОписаниеОшибки = СтрШаблон("Ошибка при установке внешней компоненты '%1': %2",
                                   МестоположениеВнешнейКомпоненты,
                                   ОписаниеОшибки());
        ВызватьИсключение ОписаниеОшибки;

    КонецПопытки;

КонецПроцедуры

// Процедура создает объекта драйвера, устанавливает параметры и подключает. Если при
// установке параметров или подключении возникла ошибка, процедура вызывает исключение. 
// 
// Параметры:
//  МестоположениеВнешнейКомпоненты - Строка - Местоположение внешней компоненты
//  Драйвер - Произвольный - Объект драйвера
Процедура ПодключитьДрайвер(Знач МестоположениеВнешнейКомпоненты, Драйвер)

    ИмяВнешнейКомпоненты = "Scanner";

    // Подключение внешней компоненты
    ВнешняяКомпонентаПодключена = ПодключитьВнешнююКомпоненту(МестоположениеВнешнейКомпоненты,
                                                              ИмяВнешнейКомпоненты,
                                                              ТипВнешнейКомпоненты.Native);

    Если Не ВнешняяКомпонентаПодключена Тогда

        ОписаниеОшибки = СтрШаблон("Ошибка при подключении внешней компоненты '%1'", МестоположениеВнешнейКомпоненты);
        ВызватьИсключение ОписаниеОшибки;

    КонецЕсли;

    // Создание драйвера
    ИмяТипаДрайвера = СтрШаблон("AddIn.%1.InputDevice", ИмяВнешнейКомпоненты);

    Попытка

        Драйвер = Новый (ИмяТипаДрайвера);

        // Настройка драйвера
        Драйвер.УстановитьПараметр("EquipmentType", "УстройствоВвода");
        Драйвер.УстановитьПараметр("BluetoothDevice", "BROADCAST");
        Драйвер.УстановитьПараметр("Action", "nlscan.action.SCANNER_RESULT"); // Сообщение Android Broadcast
        Драйвер.УстановитьПараметр("ExtraType", "String");
        Драйвер.УстановитьПараметр("Extra", "SCAN_BARCODE1"); // Содержимое сообщения

    Исключение

        ОписаниеОшибки = СтрШаблон("Ошибка при настройке драйвера '%1' при настройке внешней компоненты '%2': %3",
                                   ИмяТипаДрайвера,
                                   МестоположениеВнешнейКомпоненты,
                                   ОписаниеОшибки());
        ВызватьИсключение ОписаниеОшибки;

    КонецПопытки;

    // Подключение драйвера
    ДрайверПодключен = Драйвер.Подключить(КлючВнешнейКомпоненты());

    Если Не ДрайверПодключен Тогда

        ОписаниеОшибки = СтрШаблон("Ошибка при подключении драйвера '%1' при настройке внешней компоненты '%2'",
                                   ИмяТипаДрайвера,
                                   МестоположениеВнешнейКомпоненты);
        ВызватьИсключение ОписаниеОшибки;

    КонецЕсли;

КонецПроцедуры

#КонецЕсли

#КонецОбласти