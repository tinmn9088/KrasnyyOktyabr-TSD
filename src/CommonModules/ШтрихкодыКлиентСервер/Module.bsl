// @strict-types
#Область ПрограммныйИнтерфейс

// Функция извлекает GTIN из штрихкода.
// 
// Параметры:
//  Штрихкод - Строка - GTIN или код маркировки GS1.
// 
// Возвращаемое значение:
//  Неопределено, Строка - Строковое представление GTIN
Функция GTINИзШтрихкода(Штрихкод) Экспорт
    
    // 1. Проверка, что штрихкод содержит только GTIN (длина GTIN не более 14 символов)
    Если СтрДлина(Штрихкод) <= 14 Тогда

        Попытка

            Возврат Формат(Число(Штрихкод), "ЧГ=;");

        Исключение

            Возврат Неопределено;

        КонецПопытки;

    КонецЕсли;
    
    // 2. Попытка извлечь GTIN на случай, если штрихкод - код маркироровки (в GS1 DataMatrix начало GTIN отмечается "01")
    ПрефиксGTIN = "01";
    ПозицияПрефиксаGTIN = СтрНайти(Штрихкод, ПрефиксGTIN);

    Если ПозицияПрефиксаGTIN = 0 Тогда

        Возврат Неопределено;

    КонецЕсли;

    ПозицияGTIN = ПозицияПрефиксаGTIN + СтрДлина(ПрефиксGTIN);
    GTINСЛидирующимиНулями = Сред(Штрихкод, ПозицияGTIN, 14);

    Попытка

        Возврат Формат(Число(GTINСЛидирующимиНулями), "ЧГ=;");

    Исключение

        Возврат Неопределено;

    КонецПопытки;

КонецФункции 

// Функция удаляет из строки специальные символы FNC1 (код ASCII 232) и
// GS (код ASCII 29).
//
// Параметры:
//  КодМаркировки - Строка 
//  СимволЗамены - Строка 
// 
// Возвращаемое значение:
//  Строка 
Функция ЗаменитьСпециальныеСимволыКодаМаркировки(Знач КодМаркировки, Знач СимволЗамены = "") Экспорт
    
    // 1. Удаление ASCII 232 (FNC1)
    КодМаркировки = СтрЗаменить(КодМаркировки, Строка(Символ(232)), "");
    
    // 2. Удаление ASCII 29 (GS)  
    КодМаркировки = СтрЗаменить(КодМаркировки, Строка(Символ(29)), "");

    Возврат КодМаркировки;

КонецФункции

// Сериализовать.
// 
// Параметры:
//  Штрихкод - Строка
Процедура Сериализовать(Штрихкод) Экспорт

    Штрихкод = ЗаменитьСпециальныеСимволыКодаМаркировки(Штрихкод);
    Штрихкод = XMLКлиентСервер.ЗаменитьНедопустимыеСимволы(Штрихкод);

КонецПроцедуры

#КонецОбласти