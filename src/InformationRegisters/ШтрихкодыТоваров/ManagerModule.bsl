// @strict-types
#Область ПрограммныйИнтерфейс

// Процедура добавляет штрихкод в регистр сведений. Количество устанавливается в зависимости
// от того передана ли номенклатура. 
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
//  Номенклатура - СправочникСсылка.НоменклатураРозница, Неопределено - Номенклатура товара
Процедура ДобавитьШтрихкод(Знач Документ, Знач Штрихкод, Знач Номенклатура = Неопределено) Экспорт

    МенеджерЗаписи = СоздатьМенеджерЗаписи();

    МенеджерЗаписи.Документ = Документ;
    
    // Удаление специальных символов кода маркировки, которые могут поломать регистр сведений
    ШтрихкодыКлиентСервер.Сериализовать(Штрихкод);
    МенеджерЗаписи.Штрихкод = Штрихкод;

    Если Номенклатура <> Неопределено Тогда

        // Заполнение реквизитов для штрихкода товара
        МенеджерЗаписи.Номенклатура = Номенклатура;
        МенеджерЗаписи.Количество = 1;

    Иначе

        // Заполнение реквизитов для штрихкода без номенклатуры
        МенеджерЗаписи.Номенклатура = Справочники.НоменклатураРозница.ПустаяСсылка();
        МенеджерЗаписи.Количество = 0;

    КонецЕсли;

    МенеджерЗаписи.Записать();

КонецПроцедуры

// Процедура устанавливает количество для штрихкода.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
//  Количество - Число
Процедура УстановитьКоличество(Знач Документ, Знач Штрихкод, Знач Количество) Экспорт

    МенеджерЗаписи = МенеджерЗаписиШтрихкода(Документ, Штрихкод);

    МенеджерЗаписи.Количество = Количество;

    МенеджерЗаписи.Записать();

КонецПроцедуры

// Процедура увеличивает количество у штрихкода на 1.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
//  Номенклатура - СправочникСсылка.НоменклатураРозница, Неопределено - Номенклатура, на которую нужно изменить текущую
// 
// Возвращаемое значение:
//  Число - Новое количество
Функция УвеличитьКоличество(Знач Документ, Знач Штрихкод, Знач Номенклатура = Неопределено) Экспорт

    МенеджерЗаписи = МенеджерЗаписиШтрихкода(Документ, Штрихкод);

    НовоеКоличество = МенеджерЗаписи.Количество + 1;
    МенеджерЗаписи.Количество = НовоеКоличество;

    // Обновление номенклатуры, если параметр был передан
    Если Номенклатура <> Неопределено Тогда

        МенеджерЗаписи.Номенклатура = Номенклатура;

    КонецЕсли;

    МенеджерЗаписи.Записать();

    Возврат НовоеКоличество;

КонецФункции

// Процедура удаляет штрихкод из регистра сведений.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
Процедура УдалитьШтрихкод(Знач Документ, Знач Штрихкод) Экспорт

    МенеджерЗаписи = МенеджерЗаписиШтрихкода(Документ, Штрихкод);
    МенеджерЗаписи.Удалить();

КонецПроцедуры

// Процедура удаляет штрихкод из регистра сведений при условии, что у него не заполнена номенклатура.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
Процедура УдалитьШтрихкодБезНоменклатуры(Знач Документ, Знач Штрихкод) Экспорт

    МенеджерЗаписи = МенеджерЗаписиШтрихкода(Документ, Штрихкод);

    Если Не ЗначениеЗаполнено(МенеджерЗаписи.Номенклатура) Тогда

        МенеджерЗаписи.Удалить();

    КонецЕсли;

КонецПроцедуры

// Функция удаляет штрихкоды документа из регистра сведений.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
// 
// Возвращаемое значение:
//  Число - Количество штрихкодов, которое было удалено
Функция УдалитьШтрихкодыДокумента(Знач Документ) Экспорт

    НаборЗаписей = СоздатьНаборЗаписей();

    // Получение записей
    НаборЗаписей.Отбор.Документ.Установить(Документ);
    НаборЗаписей.Прочитать();

    КоличествоШтрихкодов = НаборЗаписей.Количество();

    // Удаление записей
    НаборЗаписей.Очистить();
    НаборЗаписей.Записать();

    Возврат КоличествоШтрихкодов;

КонецФункции

// Функция определяет есть ли штрихкод в регистре сведений.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьШтрихкод(Знач Документ, Знач Штрихкод) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                          |    ИСТИНА КАК ЕстьШтрихкод
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыТоваров КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |    И ВсеШтрихкоды.Штрихкод = &Штрихкод");

    Запрос.УстановитьПараметр("Документ", Документ);
    Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Возврат Выборка.Следующий();

КонецФункции

// Функция возвращает значение реквизита "Количество".
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
// 
// Возвращаемое значение:
//  Число
Функция КоличествоПоШтрихкоду(Знач Документ, Знач Штрихкод) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                          |    ВсеШтрихкоды.Количество
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыТоваров КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |    И ВсеШтрихкоды.Штрихкод = &Штрихкод");

    Запрос.УстановитьПараметр("Документ", Документ);
    Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        ВызватьИсключение ("Не удалось получить количество по штрихкоду");

    КонецЕсли;
    
    //@skip-check property-return-type
    Количество = Выборка.Количество; // Число

    Возврат Количество;

КонецФункции

// Функция определяет количество штрихкодов, для которых не заполнен реквизиьт "Номенклатура".
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
// 
// Возвращаемое значение:
//  Число
Функция КоличествоШтрихкодовБезНоменклатуры(Знач Документ) Экспорт

    Запрос = Новый Запрос("ВЫБРАТЬ
                          |    КОЛИЧЕСТВО(*) КАК Количество
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыТоваров КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |    И ВсеШтрихкоды.Номенклатура = ЗНАЧЕНИЕ(Справочник.НоменклатураРозница.ПустаяСсылка)");

    Запрос.УстановитьПараметр("Документ", Документ);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        ВызватьИсключение ("Не удалось получить количество штрихкодов без номенклатуры");

    КонецЕсли;
    
    //@skip-check property-return-type
    Количество = Выборка.Количество; // Число

    Возврат Количество;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция находит штрихкод в регистре сведений. Вызывает исключение, если
// не удалось найти штрихкод.
// 
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров, ДокументСсылка.ПеремещениеТоваров, ДокументСсылка.ПересчетТоваров - Документ
//  Штрихкод - Строка
//  ВызыватьИсключениеКогдаНеНайдено - Булево
// 
// Возвращаемое значение:
//  РегистрСведенийМенеджерЗаписи.ШтрихкодыТоваров
Функция МенеджерЗаписиШтрихкода(Знач Документ, Знач Штрихкод, Знач ВызыватьИсключениеКогдаНеНайдено = Истина)

    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    
    // 1. Поиск штрихкода
    МенеджерЗаписи.Документ = Документ;
    МенеджерЗаписи.Штрихкод = Штрихкод;

    МенеджерЗаписи.Прочитать();

    Если ВызыватьИсключениеКогдаНеНайдено И Не МенеджерЗаписи.Выбран() Тогда

        ВызватьИсключение (СтрШаблон("Штрихкод '%1' не найден в %2", Штрихкод, Документ));

    КонецЕсли;

    Возврат МенеджерЗаписи;

КонецФункции

#КонецОбласти