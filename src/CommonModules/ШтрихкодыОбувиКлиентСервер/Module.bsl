#Область ПрограммныйИнтерфейс

// Функция определяет тип штрихкода.
// 
// Параметры:
//  Штрихкод - Строка
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипШтрихкода, Неопределено - Тип штрихкода
Функция ТипШтрихкода(Штрихкод) Экспорт

    ДлинаШтрихкода = СтрДлина(Штрихкод);

    Если ДлинаШтрихкода <= 9 Тогда

        Возврат ?(ДлинаШтрихкода = 8 И ЕстьБуквы(Штрихкод),
                  ПредопределенноеЗначение("Перечисление.ТипШтрихкода.НомерЯчейки"),
                  ПредопределенноеЗначение("Перечисление.ТипШтрихкода.НомерКороба"));

    ИначеЕсли ДлинаШтрихкода = 19 Тогда

        Возврат ПредопределенноеЗначение("Перечисление.ТипШтрихкода.ТШК");

    ИначеЕсли ДлинаШтрихкода >= 31 Тогда

        Возврат ПредопределенноеЗначение("Перечисление.ТипШтрихкода.КодМаркировки");

    КонецЕсли;

    Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция проверяет содержит ли строка символы алфавита. 
// 
// Параметры:
//  Значение - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьБуквы(Значение)

    Возврат ВРег(Значение) <> НРег(Значение);

КонецФункции

#КонецОбласти