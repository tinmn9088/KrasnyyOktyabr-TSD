// @strict-types
#Область ПрограммныйИнтерфейс

// Процедура показывает обычное (всплывает снизу) оповещение пользователя.
// 
// Параметры:
//  Заголовок - Строка - Заголовок оповещения
//  Пояснение - Строка - Пояснение оповещения
//  КлючУникальности - Строка - Ключ уникальности
Процедура ПоказатьОповещение(Заголовок, Пояснение, КлючУникальности) Экспорт

    ПоказатьОповещениеПользователя(Заголовок, , Пояснение, , , КлючУникальности);

КонецПроцедуры

// См. ПоказатьВажноеОповещение
Процедура ПоказатьВажноеОповещениеИнформация(Заголовок,
                                             Пояснение,
                                             КлючУникальности,
                                             НавигационнаяСсылка = Неопределено) Экспорт

    ПоказатьВажноеОповещение(Заголовок, Пояснение, БиблиотекаКартинок.Информация, КлючУникальности, НавигационнаяСсылка);

КонецПроцедуры      

// См. ПоказатьВажноеОповещение
Процедура ПоказатьВажноеОповещениеУспех(Заголовок, Пояснение, КлючУникальности, НавигационнаяСсылка = Неопределено) Экспорт

    ПоказатьВажноеОповещение(Заголовок,
                             Пояснение,
                             БиблиотекаКартинок.ОформлениеЗнакФлажок,
                             КлючУникальности,
                             НавигационнаяСсылка);

КонецПроцедуры  

// См. ПоказатьВажноеОповещение
Процедура ПоказатьВажноеОповещениеОшибка(Заголовок, Пояснение, КлючУникальности, НавигационнаяСсылка = Неопределено) Экспорт

    ПоказатьВажноеОповещение(Заголовок,
                             Пояснение,
                             БиблиотекаКартинок.ОформлениеЗнакКрест,
                             КлючУникальности,
                             НавигационнаяСсылка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура показывает важное (всплывает сверху) оповещение пользователя с иконкой.
// 
// Параметры:
//  Заголовок - Строка - Заголовок оповещения
//  Пояснение - Строка - Пояснение оповещения
//  Картинка - Картинка - Картинка оповещения
//  КлючУникальности - Строка - Ключ уникальности
//  НавигационнаяСсылка - Строка - Действие при нажатии
Процедура ПоказатьВажноеОповещение(Заголовок, Пояснение, Картинка, КлючУникальности, НавигационнаяСсылка = Неопределено)

    СтатусОповещения = Неопределено;

#Если Не МобильноеПриложениеКлиент Тогда

    СтатусОповещения = СтатусОповещенияПользователя.Важное;

#КонецЕсли

    ПоказатьОповещениеПользователя(Заголовок,
                                   НавигационнаяСсылка,
                                   Пояснение,
                                   Картинка,
                                   СтатусОповещения,
                                   КлючУникальности);

КонецПроцедуры

#КонецОбласти