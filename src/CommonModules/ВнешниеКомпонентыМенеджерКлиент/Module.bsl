// @strict-types
#Область ПрограммныйИнтерфейс

#Область Конструкторы

// Функция создает новую коллекцию для внешних компонент.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - Коллекция для внешних компонент:
// * Ключ - Строка
// * Значение - Произвольный
Функция СоздатьКоллекциюВнешнихКомпонент() Экспорт

    Возврат Новый Соответствие;

КонецФункции

// Функция создает описание внешнего события.
// 
// Возвращаемое значение:
//  Структура - Описание внешнего события:
// * Источник - Строка - Источник события
// * Событие - Строка - Имя события
// * Данные - Строка - Данные события
Функция СоздатьОписаниеВнешнегоСобытия() Экспорт

    Возврат Новый Структура("Источник, Событие, Данные", "", "", "");

КонецФункции

#КонецОбласти

// Функция устанавливает и подключает внешние компоненты. Если при установке
// или подключении внешней компоненты возникает ошибка, функция вызывает исключение.
// 
// Возвращаемое значение: См. СоздатьКоллекциюВнешнихКомпонент
Функция НастроитьВнешниеКомпоненты() Экспорт

    // Создание коллекции для хранения внешних компонент
    ВнешниеКомпоненты = СоздатьКоллекциюВнешнихКомпонент();

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

    // Добавление в коллекцию и настройка драйвера для сканера Newland N7 Cachalot Pro
    СканерШтрихкодаNewlandN7CachalotProКлиент.НастроитьДрайвер(ВнешниеКомпоненты);

#КонецЕсли

    // Возврат коллекции подключенных внешних компонент
    Возврат ВнешниеКомпоненты;

КонецФункции

// Процедура обрабатывает сообщения внешних компонент.
// 
// Параметры:
//  ОписаниеВнешнегоСобытия - См. СоздатьОписаниеВнешнегоСобытия
//  ВнешниеКомпоненты - См. СоздатьКоллекциюВнешнихКомпонент
Процедура ОбработатьВнешнееСобытие(ОписаниеВнешнегоСобытия, ВнешниеКомпоненты) Экспорт

    Если ЗначениеЗаполнено(ВнешниеКомпоненты) Тогда

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

		// Проверка на то, что драйвер сканера Newland N7 Cachalot Pro был настроен
        Если ВнешниеКомпоненты.Получить(СканерШтрихкодаNewlandN7CachalotProКлиент.ПолучитьИмя()) <> Неопределено Тогда

    		// Обработка сканирования штрихкода на сканере Newland N7 Cachalot Pro
            СканерШтрихкодаNewlandN7CachalotProКлиент.ОбработатьВнешнееСобытие(ОписаниеВнешнегоСобытия);
            Возврат;

        КонецЕсли;

#КонецЕсли

    КонецЕсли;

КонецПроцедуры

// Процедура отключает внешние компоненты.
// 
// Параметры:
//  ВнешниеКомпоненты - См. СоздатьКоллекциюВнешнихКомпонент
Процедура ОтключитьВнешниеКомпоненты(ВнешниеКомпоненты) Экспорт

    Для Каждого КлючИЗначение Из ВнешниеКомпоненты Цикл

        ИмяВнешнейКомпоненты = КлючИЗначение.Ключ;
        ВнешняяКомпонента = КлючИЗначение.Значение;

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

        Если ИмяВнешнейКомпоненты = СканерШтрихкодаNewlandN7CachalotProКлиент.ПолучитьИмя() Тогда

            // Отключение драйвера для сканера Newland N7 Cachalot Pro
            СканерШтрихкодаNewlandN7CachalotProКлиент.ОтключитьДрайвер(ВнешняяКомпонента);

        КонецЕсли;

#КонецЕсли

    КонецЦикла;

КонецПроцедуры

#КонецОбласти