// @strict-types
#Область ПрограммныйИнтерфейс

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

// Процедура воспроизводит звуковой сигнал.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
Процедура ВоспроизвестиЗвуковоеОповещениеДляКодаВозврата(Знач КодВозврата) Экспорт

    Звук = ЗвукПоКодуВозврата(КодВозврата);
    Вибрация = Истина;

    СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение(Звук, Вибрация);

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал, соответствующий ошибке сканирования штрихкода.
Процедура ВоспроизвестиЗвуковоеОповещениеОбОшибкеСканированияШтрихкода() Экспорт

    Звук = "oshibka";
    Вибрация = Истина;

    СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение(Звук, Вибрация);

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал, соответствующий успешному сканированию штрихкода.
// 
// Параметры:
//  Тип - Неопределено, ПеречислениеСсылка.ТипШтрихкода - Тип штрихкода
Процедура ВоспроизвестиЗвуковоеОповещениеОбУспешномСканированииШтрихкода(Знач Тип = Неопределено) Экспорт

    // Выбор звукового сигнала в зависимости от типа штрихкода
    Звук = "barcode";

    Если Тип = ПредопределенноеЗначение("Перечисление.ТипШтрихкода.ТШК") Тогда

        Звук = "barcode";

    ИначеЕсли Тип = ПредопределенноеЗначение("Перечисление.ТипШтрихкода.DataMatrix") Тогда

        Звук = "datamatrix";

    КонецЕсли;

    Вибрация = Истина;

    СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение(Звук, Вибрация);

КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает имя звука, соответствующего коду возврата.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
// 
// Возвращаемое значение:
//  Неопределено, Строка - Звук, соответствующий коду возврата, или Неопределено
Функция ЗвукПоКодуВозврата(Знач КодВозврата)

    Если ЭтоКодВозвратаУспех(КодВозврата) Тогда

        Возврат "barcode";

    ИначеЕсли ЭтоКодВозвратаНеНайденоЗаданиеНаОтборПоШтрихкоду(КодВозврата) Тогда

        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаСлишкомМногоВКоробе(КодВозврата) Тогда

        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаОтсутствуетДокументНаВнутреннееПеремещение(КодВозврата) Тогда

        Возврат "oshibka";

    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеПроведен(КодВозврата) Тогда

        Возврат "povtor";

    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеПроведенДругим(КодВозврата) Тогда

        Возврат "chyzhoi";

    ИначеЕсли ЭтоКодВозвратаНеНайденКодМаркировки(КодВозврата) Тогда

        Возврат "oshibka";
        
    ИначеЕсли ЭтоКодВозвратаКодМаркировкиУжеПроверен(КодВозврата) Тогда

        Возврат "povtor";

    Иначе

        Возврат Неопределено;

    КонецЕсли;

КонецФункции

#КонецОбласти