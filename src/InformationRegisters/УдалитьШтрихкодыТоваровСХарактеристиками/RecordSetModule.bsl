// @strict-types
#Область ПрограммныйИнтерфейс

// Процедура удаляет штрихкод из регистра сведений.
// 
// Параметры:
//  Документ - ДокументСсылка.ПересчетТоваров
//  Номенклатура - СправочникСсылка.НоменклатураРозница
//  УникальныйИдентификаторХарактеристики - УникальныйИдентификатор
Процедура УдалитьТоварСХарактеристикой(Знач Документ, Знач Номенклатура, Знач УникальныйИдентификаторХарактеристики) Экспорт

    Отбор.Документ.Установить(Документ);
    Отбор.УникальныйИдентификаторХарактеристики.Установить(УникальныйИдентификаторХарактеристики);
    Прочитать();

    ИндексыЗаписейДляУдаления = Новый Массив; // Массив из Число

    Для ИндексЗаписи = 0 По Количество() - 1 Цикл

        Если Получить(ИндексЗаписи).Номенклатура = Номенклатура Тогда

            ИндексыЗаписейДляУдаления.Вставить(0, ИндексЗаписи);

        КонецЕсли;

    КонецЦикла;

    Для Каждого Индекс Из ИндексыЗаписейДляУдаления Цикл

        Удалить(Индекс);

    КонецЦикла;

    Записать();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура обновляет реквизит ДатаИзменения.
// 
// Параметры:
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура ПередЗаписью(Отказ, Замещение)

    Если ОбменДанными.Загрузка Тогда

        Возврат;

    КонецЕсли;

    Для Каждого Запись Из ЭтотОбъект Цикл

        Запись.ДатаИзменения = ТекущаяДатаСеанса();

    КонецЦикла;

КонецПроцедуры

#КонецОбласти