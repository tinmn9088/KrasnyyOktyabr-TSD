// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаДокумент = Параметры.Документ;
    ФормаЗаполнениеЗавершено = ВыполнениеРеализацииЗавершено(ФормаДокумент);
    ФормаСотрудник = Параметры.Сотрудник;

    ФормаСписокТоваров.Параметры.УстановитьЗначениеПараметра("Документ", ФормаДокумент);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    Если ФормаЗаполнениеЗавершено Тогда

        ПоказатьЗаполнениеЗавершено();

    КонецЕсли;

    ОбновитьКнопкуУдаленияШтрихкодовБезНоменклатуры();

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    Элементы.КонтрагентНадпись.Видимость = Истина;
    Элементы.ПокупательКонтрагент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;

    Элементы.ПродавецНадпись.Видимость = Истина;
    Элементы.ПродавецМагазин.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
    Элементы.ПродавецСклад.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;

    Элементы.СписокТоваров.Шапка = Ложь;

#КонецЕсли

    // Заполнение заголовка формы
    Заголовок = СтрШаблон("Выполнение: %1", ФормаДокумент);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

    Если ВводДоступен() И ИмяСобытия = ОповещенияФормыКлиент.ИмяСобытияСканированияШтрихкода() Тогда

        Штрихкод = Строка(Параметр);

        ОбработатьШтрихкод(Штрихкод);

    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

    Если ТипЗнч(ИсточникВыбора) = Тип("ФормаКлиентскогоПриложения") Тогда

        Если ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ФормаВводаКоличестваТовара" Тогда

            Результат = ВыбранноеЗначение; // См. ОповещенияФормыКлиент.ОписаниеРезультатаИзмененияКоличестваТовара
            Штрихкод = Результат.ИдентификаторТовара;
            ВыбраноУдаление = Результат.ВыбраноУдаление;
    
            // Удаление
            Если ВыбраноУдаление Тогда

                УдалитьТовар(ФормаДокумент, Штрихкод);

                ОповещенияПользователяКлиент.ПоказатьОповещение("Товар удален",
                                                                "",
                                                                КлючУникальностиОповещенийПользователя());

                ОбновитьФорму();

            // Обновление количества
            Иначе

                ПредыдущееКоличество = Результат.ИсходноеЗначение;
                НовоеКоличество = Результат.НовоеЗначение;

                Если НовоеКоличество <> ПредыдущееКоличество Тогда

                    ОбновитьКоличествоТовара(ФормаДокумент, Штрихкод, НовоеКоличество);
                    ОбновитьФорму();

                КонецЕсли;

            КонецЕсли;

        КонецЕсли;

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокТоваров

// Процедура обрабатывает выбор строки в списке товаров.
// 
// Параметры:
//  Элемент - ТаблицаФормы
//  ВыбраннаяСтрока - Строка - Штрихкод
//  Поле - ПолеФормы
//  СтандартнаяОбработка - Булево
&НаКлиенте
Асинх Процедура СписокТоваровВыборАсинх(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

    ПредыдущееКоличество = КоличествоТовараВРеализации(ФормаДокумент, ВыбраннаяСтрока);

    // Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
    Если ПредыдущееКоличество = Неопределено Тогда
        
        // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
        Возврат;

    КонецЕсли;

    // Открытие формы ввода количества
    ПараметрыФормы = Новый Структура;

    ПараметрыФормы.Вставить("ИдентификаторТовара", ВыбраннаяСтрока);
    ПараметрыФормы.Вставить("ИсходноеЗначение", ПредыдущееКоличество);
    ПараметрыФормы.Вставить("ВозможностьУдаления", Истина);

    ОткрытьФорму("ОбщаяФорма.ФормаВводаКоличестваТовара", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ОбработатьШтрихкодСФормыАсинх(Команда)

    Значение = Ждать ВвестиСтрокуАсинх(ФормаШтрихкод, "Введите штрихкод");

    Если Не ЗначениеЗаполнено(Значение) Тогда

        Возврат;

    КонецЕсли;

    ФормаШтрихкод = Значение;

    ОбработатьШтрихкод(ФормаШтрихкод);

КонецПроцедуры

&НаКлиенте
Асинх Процедура УдалитьШтрихкодБезНоменклатурыАсинх(Команда)
    
    // 1. Подготовка списка штрихкодов без номенклатуры
    СписокШтрихкодовБезНоменклатуры = ШтрихкодыБезНоменклатуры(ФормаДокумент);
    
    // Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
    Если СписокШтрихкодовБезНоменклатуры = Неопределено Тогда
        
        // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
        Возврат;

    КонецЕсли;
    
    // 2. Выбор штрихкода для удаления
    ВыбранныйЭлемент = Ждать СписокШтрихкодовБезНоменклатуры.ВыбратьЭлементАсинх("Удалить штрихкод");

    Если ВыбранныйЭлемент = Неопределено Тогда

        Возврат;

    КонецЕсли;

    ВыбранныйШтрихкод = Строка(ВыбранныйЭлемент.Значение);

    // 3. Подтверждение удаления штрихкода
    ОтветНаВопрос = Ждать ВопросАсинх(ВыбранныйШтрихкод, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, "Удалить штрихкод?"); // КодВозвратаДиалога

    Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда

        Возврат;

    КонецЕсли;

    // 4. Удаление штрихкода
    УдалитьШтрихкодБезНоменклатуры(ФормаДокумент, ВыбранныйШтрихкод);

    // Нельзя обновлять список товаров, т.к. это вызывает ошибку на мобильном клиенте
    ОбновитьФорму(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеРеализацииТоваров

// Функция определяет завершено ли выполнение.
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
// 
// Возвращаемое значение:
//  Булево
&НаСервереБезКонтекста
Функция ВыполнениеРеализацииЗавершено(Знач РеализацияТоваров)

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
                          |   РеализацияТоваров.ПометкаУдаления КАК ВыполнениеЗавершено
                          |ИЗ
                          |   Документ.РеализацияТоваров КАК РеализацияТоваров
                          |ГДЕ
                          |   РеализацияТоваров.Ссылка = &Документ");

    Запрос.УстановитьПараметр("Документ", РеализацияТоваров);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        ВызватьИсключение ("Не удалось определить выполнена ли реализация");

    КонецЕсли;

    //@skip-check property-return-type
    ВыполнениеЗавершено = Выборка.ВыполнениеЗавершено; // Булево

    Возврат ВыполнениеЗавершено;

КонецФункции

// Процедура обрабатывает штрихкод.
// 
// Параметры:
//  Штрихкод - Строка
&НаКлиенте
Процедура ОбработатьШтрихкод(Штрихкод)

    Перем ОписаниеОбработкиШтрихкода; // См. Перемещения.ОбработатьШтрихкодТовара

    // 1. Оповещение пользователю о начале обработки штрихкода
    ПоказатьОповещение("Обработка штрихкода", Штрихкод);
                                                    
    // 2.1. Обработка штрихкода
    
    // Попытка извлечь GTIN из штрихкода
    GTIN = GTINИзШтрихкода(Штрихкод);

    ОписаниеОбработкиШтрихкода =  ОбработатьШтрихкодТовара(ФормаДокумент, ?(ЗначениеЗаполнено(GTIN), GTIN, Штрихкод));       
                                   
    // 2.2. Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
    Если ОписаниеОбработкиШтрихкода = Неопределено Тогда
        
        // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
        Возврат;

    КонецЕсли;
                                              
    // 3.1. Оповещение об успешной обработке
    Если ЭтоКодВозвратаТоварНайден(ОписаниеОбработкиШтрихкода.КодВозврата) Тогда

        ПояснениеОповещения = СтрШаблон("Количество %1 → %2",
                                        ОписаниеОбработкиШтрихкода.ПредыдущееКоличество,
                                        ОписаниеОбработкиШтрихкода.НовоеКоличество);

        ПоказатьОповещениеПоКодуВозврата(ОписаниеОбработкиШтрихкода.КодВозврата, "Товар", ПояснениеОповещения);

        ОбновитьФорму();
                                                        
    // 3.2. Если не найдена номенклатура, предложить сохранить штрихкод без номенклатуры
    ИначеЕсли ЭтоКодВозвратаНовыйШтрихкод(ОписаниеОбработкиШтрихкода.КодВозврата) Тогда

        ПоказатьОповещениеПоКодуВозврата(ОписаниеОбработкиШтрихкода.КодВозврата, "Новый штрихкод");

        ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьОтветНаВопросОСохраненииШтрихкодаБезНоменклатуры",
                                                ЭтотОбъект,
                                                Штрихкод),
                       "Номенклатура со штрихкодом не найдена, но можно будет создать новую карточку позднее.",
                       РежимДиалогаВопрос.ДаНет,
                       ,
                       КодВозвратаДиалога.Да,
                       "Сохранить штрихкод?");

    КонецЕсли;

КонецПроцедуры

// Процедура сохраняет штрихкод без номенклатуры.
// 
// Параметры:
//  РезультатВопроса - КодВозвратаДиалога
//  ДополнительныеПараметры - Строка
&НаКлиенте
Процедура ОбработатьОтветНаВопросОСохраненииШтрихкодаБезНоменклатуры(РезультатВопроса, ДополнительныеПараметры) Экспорт

    // 1. Проверка, что пользователь не нажал кнопку "Отмена"
    Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда

        Возврат;

    КонецЕсли;

    // 2. Обработка штрихкода
    Штрихкод = ДополнительныеПараметры;
    ОписаниеОбработкиШтрихкода = ОбработатьШтрихкодБезНоменклатуры(ФормаДокумент, Штрихкод);
    
    // 3.1. Оповещение об успешной обработке
    Если ЭтоКодВозвратаУспех(ОписаниеОбработкиШтрихкода.КодВозврата) Тогда

        ПоказатьОповещение("Штрихкод без номенклатуры", ДополнительныеПараметры);

        ОбновитьФорму();
                                                        
    // 3.2. Оповещение об ошибке
    Иначе

        ПоказатьВажноеОповещениеОбОшибкеДляКодаВозврата(ОписаниеОбработкиШтрихкода.КодВозврата,
                                                        "Не удалось сохранить штрихкод без номенклатуры",
                                                        Штрихкод);

    КонецЕсли;

    ОбновитьФорму();

КонецПроцедуры

// См. ШтрихкодыКлиентСервер.GTINИзШтрихкода
&НаКлиенте
Функция GTINИзШтрихкода(Штрихкод)

    Возврат ШтрихкодыКлиентСервер.GTINИзШтрихкода(Штрихкод);

КонецФункции 

// См. Реализация.ОбработатьШтрихкодТовара
&НаСервереБезКонтекста
Функция ОбработатьШтрихкодТовара(РеализацияТоваров, ШтрихкодТовара)

    Возврат Реализация.ОбработатьШтрихкодТовара(РеализацияТоваров, ШтрихкодТовара);

КонецФункции

// См. Реализация.ОбработатьШтрихкодБезНоменклатуры
&НаСервереБезКонтекста
Функция ОбработатьШтрихкодБезНоменклатуры(РеализацияТоваров, ШтрихкодБезНоменклатуры)

    Возврат Реализация.ОбработатьШтрихкодБезНоменклатуры(РеализацияТоваров, ШтрихкодБезНоменклатуры);

КонецФункции

// См. РегистрыСведений.ШтрихкодыТоваров.
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
//  Штрихкод - Строка
&НаСервереБезКонтекста
Функция КоличествоТовараВРеализации(Знач РеализацияТоваров, Знач Штрихкод)

    Возврат РегистрыСведений.ШтрихкодыТоваров.КоличествоПоШтрихкоду(РеализацияТоваров, Штрихкод);

КонецФункции

// См. РегистрыСведений.ШтрихкодыТоваров.УстановитьКоличество
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
//  Штрихкод - Строка
//  НовоеКоличествоТовара - Число
&НаСервереБезКонтекста
Процедура ОбновитьКоличествоТовара(Знач РеализацияТоваров, Знач Штрихкод, Знач НовоеКоличествоТовара)

    РегистрыСведений.ШтрихкодыТоваров.УстановитьКоличество(РеализацияТоваров, Штрихкод, НовоеКоличествоТовара);

КонецПроцедуры

// См. РегистрыСведений.ШтрихкодыТоваров.УдалитьШтрихкод
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
//  Штрихкод - Строка
&НаСервереБезКонтекста
Процедура УдалитьТовар(Знач РеализацияТоваров, Знач Штрихкод)

    РегистрыСведений.ШтрихкодыТоваров.УдалитьШтрихкод(РеализацияТоваров, Штрихкод);

КонецПроцедуры

// См. РегистрыСведений.ШтрихкодыТоваров.УдалитьШтрихкодБезНоменклатуры
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
//  ШтрихкодБезНоменклатуры - Строка
&НаСервереБезКонтекста
Процедура УдалитьШтрихкодБезНоменклатуры(Знач РеализацияТоваров, Знач ШтрихкодБезНоменклатуры)

    РегистрыСведений.ШтрихкодыТоваров.УдалитьШтрихкодБезНоменклатуры(РеализацияТоваров, ШтрихкодБезНоменклатуры);

КонецПроцедуры

// См. РегистрСведенийМенеджер.ШтрихкодыТоваров.КоличествоШтрихкодовБезНоменклатуры
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
//  
// Возвращаемое значение:
//  Число
&НаСервереБезКонтекста
Функция КоличествоШтрихкодовБезНоменклатуры(РеализацияТоваров)

    Возврат РегистрыСведений.ШтрихкодыТоваров.КоличествоШтрихкодовБезНоменклатуры(РеализацияТоваров);

КонецФункции

// Функция возвращает список из 50 штрихкодов без номенклатуры.
// 
// Параметры:
//  РеализацияТоваров - ДокументСсылка.РеализацияТоваров
// 
// Возвращаемое значение:
// СписокЗначений из Строка
&НаСервереБезКонтекста
Функция ШтрихкодыБезНоменклатуры(РеализацияТоваров)

    Штрихкоды = Новый СписокЗначений; // СписокЗначений из Строка

    Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 50
                          |    ВсеШтрихкоды.Штрихкод
                          |ИЗ
                          |    РегистрСведений.ШтрихкодыТоваров КАК ВсеШтрихкоды
                          |ГДЕ
                          |    ВсеШтрихкоды.Документ = &Документ
                          |    И ВсеШтрихкоды.Номенклатура = ЗНАЧЕНИЕ(Справочник.НоменклатураРозница.ПустаяСсылка)");

    Запрос.УстановитьПараметр("Документ", РеализацияТоваров);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Пока Выборка.Следующий() Цикл
        
        //@skip-check property-return-type
        Штрихкод = Выборка.Штрихкод; // Строка

        Штрихкоды.Добавить(Штрихкод);

    КонецЦикла;

    Возврат Штрихкоды;

КонецФункции

#КонецОбласти

#Область ОповещенияПользователя

// Функция возвращает ключ уникальности оповещений пользователя.
// 
// Возвращаемое значение:
//  Строка
&НаКлиенте
Функция КлючУникальностиОповещенийПользователя()

    Возврат ИмяФормы;

КонецФункции

// См. ОповещенияПользователяКлиент.ПоказатьОповещение
// 
// Параметры:
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьОповещение(Знач Заголовок, Знач Пояснение)

    ОповещенияПользователяКлиент.ПоказатьОповещение(Заголовок, Пояснение, КлючУникальностиОповещенийПользователя());

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал, соответствующий коду возврата и 
// показывает оповещение.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьОповещениеПоКодуВозврата(Знач КодВозврата, Знач Заголовок, Знач Пояснение = "")

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеДляКодаВозврата(КодВозврата);

#КонецЕсли

    ОповещенияПользователяКлиент.ПоказатьОповещение(Заголовок, Пояснение, КлючУникальностиОповещенийПользователя());

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал, соответствующий коду возврата и 
// показывает важное оповещение об ошибке.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьВажноеОповещениеОбОшибкеДляКодаВозврата(Знач КодВозврата, Знач Заголовок, Знач Пояснение)

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеДляКодаВозврата(КодВозврата);

#КонецЕсли

    ОповещенияПользователяКлиент.ПоказатьВажноеОповещениеОшибка(Заголовок,
                                                                Пояснение,
                                                                КлючУникальностиОповещенийПользователя());

КонецПроцедуры

#КонецОбласти

#Область Форма

&НаКлиенте
Процедура ПоказатьЗаполнениеЗавершено()

    Элементы.СписокТоваров.Видимость = Ложь;
    Элементы.КнопкаВвестиШтрихкод.Видимость = Ложь;

    Элементы.ПодсказкаВыполнениеЗавершено.Видимость = Истина;

    Элементы.Сотрудник.Заголовок = "Заполнил";

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКнопкуУдаленияШтрихкодовБезНоменклатуры()

    КоличествоШтрихкодовБезНоменклатуры = КоличествоШтрихкодовБезНоменклатуры(ФормаДокумент);
    
    // Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
    Если КоличествоШтрихкодовБезНоменклатуры = Неопределено Тогда
        
        // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
        Возврат;

    КонецЕсли;

    ПоказатьКнопку = КоличествоШтрихкодовБезНоменклатуры > 0;

    // Приходится скрывать поле формы с названием документа, чтобы в автономном режиме
    // не появилась полоса прокрутки
    Элементы.Документ.Видимость = Не ПоказатьКнопку;

    Элементы.КнопкаУдалитьШтрихкодБезНоменклатуры.Видимость = ПоказатьКнопку;
    Элементы.КнопкаУдалитьШтрихкодБезНоменклатуры.Заголовок = СтрШаблон("Штрихкоды без номенклатуры (%1)",
                                                                        КоличествоШтрихкодовБезНоменклатуры);

КонецПроцедуры

// Процедура обновляет список товаров и кнопку удаления штрихкодов без номенклатуры.
&НаКлиенте
Процедура ОбновитьФорму(ОбновитьСписокТоваров = Истина)

    ОбновитьКнопкуУдаленияШтрихкодовБезНоменклатуры();

    Если ОбновитьСписокТоваров Тогда

        Элементы.СписокТоваров.Обновить();

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти