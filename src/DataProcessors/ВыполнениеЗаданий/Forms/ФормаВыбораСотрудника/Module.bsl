// @strict-types
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // Отобразить подсказку про возможность отсканировать штрихкод с табельным
    // номером, если на клиенте настроено сканирование штрихкодов
    Элементы.ПодсказкаОтсканируйтеШтрихкод.Видимость = ВнешниеКомпонентыКлиент.НастроеноСканированиеШтрихкодов();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

    Если ВводДоступен() И ИмяСобытия = ОповещенияФормыКлиент.ИмяСобытияСканированияШтрихкода() Тогда

        // 1. Получение табельного номера из отсканированного штрихкода
        Штрихкод = Строка(Параметр);    
	  
        // 2.1. Проверка длины штрихкода
        Если СтрДлина(Штрихкод) > 20 Тогда

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

            СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеОбОшибкеСканированияШтрихкода();

#КонецЕсли

            ОповещенияПользователяКлиент.ПоказатьВажноеОповещениеОшибка("Неверный штрихкод",
                                                                        "Длиннее 20 символов",
                                                                        КлючУникальностиОповещенийПользователя());
            Возврат;

        КонецЕсли
        ;
        
        // 3. Заполнение поля ввода табельного номера
        ФормаТабельныйНомер = Штрихкод;
        ОбновитьОтображениеДанных(Элементы.ТабельныйНомер);
        Элементы.КнопкаПоказатьЗадания.Доступность = ЗначениеЗаполнено(Штрихкод);
        
        // 4. Открытие выбора задания для сотрудника
#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

        СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеОбУспешномСканированииШтрихкода();

#КонецЕсли

        ОповещенияПользователяКлиент.ПоказатьОповещение("Табельный номер отсканирован",
                                                        Штрихкод,
                                                        КлючУникальностиОповещенийПользователя());

        ОткрытьВыборЗадания(Штрихкод);

    КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФормаТабельныйНомерИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)

    Элементы.КнопкаПоказатьЗадания.Доступность = ЗначениеЗаполнено(Текст);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура открывает выбор задания для сотрудника с табельным номером из поля ввода.
// 
// Параметры:
//  Команда - КомандаФормы
&НаКлиенте
Процедура ОткрытьВыборЗаданияСФормы(Команда)

    ОткрытьВыборЗадания(ФормаТабельныйНомер);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает ключ уникальности оповещений пользователя.
// 
// Возвращаемое значение:
//  Строка
&НаКлиенте
Функция КлючУникальностиОповещенийПользователя()

    Возврат ИмяФормы;

КонецФункции

// Процедура выполняет поиск сотрудника по табельному номеру и открывает форму выбора задания,
// если сотрудник найден.
// 
// Параметры:
//  ТабельныйНомер - Строка
&НаКлиенте
Процедура ОткрытьВыборЗадания(Знач ТабельныйНомер)

    // 1. Поиск сотрудника по введенному табельному номеру
    Сотрудник = НайтиСотрудникаПоТабельномуНомеру(ТабельныйНомер);
    
    // 2. Проверка, что сотрудник был найден
    Если Не ЗначениеЗаполнено(Сотрудник) Тогда

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда

        СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеОбОшибкеСканированияШтрихкода();

#КонецЕсли

        ОповещенияПользователяКлиент.ПоказатьВажноеОповещениеОшибка("Сотрудник не найден",
                                                                    ТабельныйНомер,
                                                                    КлючУникальностиОповещенийПользователя());
        Возврат;

    КонецЕсли
    ;
    
    // 3. Открытие формы выбора задания в зависимости от вида сотрудника
    Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Комплектовщики") Тогда

        ПараметрыФормы = Новый Структура("Комплектовщик", Сотрудник);

        ОткрытьФорму("Документ.Задание.Форма.ФормаВыбораЗаданияДляКомплектовщика", ПараметрыФормы);

    ИначеЕсли ТипЗнч(Сотрудник) = Тип("СправочникСсылка.СотрудникиСклада") Тогда

        ПараметрыФормы = Новый Структура("Сотрудник", Сотрудник);

        ОткрытьФорму("Обработка.ВыполнениеЗаданий.Форма.ФормаВыбораПроцесса", ПараметрыФормы);

    КонецЕсли;

КонецПроцедуры

// Функция выполняет поиск сотрдуника по табельному номеру.
// 
// Параметры:
//  ТабельныйНомер - Строка
// 
// Возвращаемое значение:
//  Неопределено, СправочникСсылка.Комплектовщики, СправочникСсылка.СотрудникиСклада - Сотрудник с нужным табельным номером
&НаСервере
Функция НайтиСотрудникаПоТабельномуНомеру(Знач ТабельныйНомер)

    Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
                          |	Комплектовщики.Ссылка КАК Сотрудник
                          |ИЗ
                          |	Справочник.Комплектовщики КАК Комплектовщики
                          |ГДЕ
                          |	Комплектовщики.Идентификатор = &ТабельныйНомер
                          |
                          |ОБЪЕДИНИТЬ ВСЕ
                          |
                          |ВЫБРАТЬ ПЕРВЫЕ 1
                          |	СотрудникиСклада.Ссылка
                          |ИЗ
                          |	Справочник.СотрудникиСклада КАК СотрудникиСклада
                          |ГДЕ
                          |	СотрудникиСклада.ТабельныйНомер = &ТабельныйНомер");

    Запрос.УстановитьПараметр("ТабельныйНомер", ТабельныйНомер);
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Если Не Выборка.Следующий() Тогда

        Возврат Неопределено;

    КонецЕсли;
    
    //@skip-check property-return-type
    Сотрудник = Выборка.Сотрудник; // СправочникСсылка.Комплектовщики, СправочникСсылка.СотрудникиСклада

    Возврат Сотрудник;

КонецФункции

#КонецОбласти