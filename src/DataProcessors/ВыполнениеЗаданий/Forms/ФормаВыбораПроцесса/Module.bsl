// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаСотрудникСклада = Параметры.СотрудникСклада;

    ФормаДоступенПриход = ТекущийПользовательИмеетПравоВыполнятьПриходы();
    ФормаДоступноПеремещение = ТекущийПользовательИмеетПравоВыполнятьПеремещения();
    ФормаДоступнаИнвентаризация = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    Если Не ФормаДоступенПриход Тогда

        СделатьКнопкуНедоступной(Элементы.КнопкаВыбратьПриход);

    КонецЕсли;

    Если Не ФормаДоступноПеремещение Тогда

        СделатьКнопкуНедоступной(Элементы.КнопкаВыбратьПеремещение);

    КонецЕсли;

    Если Не ФормаДоступнаИнвентаризация Тогда

        СделатьКнопкуНедоступной(Элементы.КнопкаВыбратьИнвентаризацию);

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПроцессПриход(Команда)

#Если Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда

    Закрыть();

#КонецЕсли
   
    // Открытие формы выбора 
    ПараметрыФормы = Новый Структура("Сотрудник", ФормаСотрудникСклада);
    ОткрытьФорму("Обработка.ПроверкаЭлектронныхНакладных.Форма.ФормаВыбораЭлектроннойНакладнойДляПроверки",
                 ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроцессПеремещение(Команда)

#Если Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда

    Закрыть();

#КонецЕсли
   
    // Открытие формы выбора 
    ПараметрыФормы = Новый Структура("Сотрудник", ФормаСотрудникСклада);
    ОткрытьФорму("Обработка.ЗаполнениеПеремещений.Форма.ФормаВыбораПеремещения", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроцессИнвентаризацию(Команда)

    ОповещенияПользователяКлиент.ПоказатьОповещение("Процесс не поддерживается", "Инвентаризация", ИмяФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ТекущийПользовательИмеетПравоВыполнятьПриходы()

    Возврат ПравоДоступа("Использование", Метаданные.Обработки.ПроверкаЭлектронныхНакладных);

КонецФункции

&НаСервере
Функция ТекущийПользовательИмеетПравоВыполнятьПеремещения()

    Возврат ПравоДоступа("Использование", Метаданные.Обработки.ЗаполнениеПеремещений);

КонецФункции

// Процедура добавляет астериск в начале заголовка и отключает доступность кнопки.
// 
// Параметры:
//  Кнопка - КнопкаФормы
&НаКлиенте
Процедура СделатьКнопкуНедоступной(Кнопка)

    Кнопка.Заголовок = "* " + Кнопка.Заголовок;
    Кнопка.Доступность = Ложь;

КонецПроцедуры

#КонецОбласти