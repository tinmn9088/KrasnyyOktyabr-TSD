// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    ФормаКомплектовщик = Параметры.Комплектовщик;
    ФормаПроцесс = Параметры.Процесс;

    ФормаСписокШтрихкодов.Параметры.УстановитьЗначениеПараметра("Комплектовщик", ФормаКомплектовщик);
    ФормаСписокШтрихкодов.Параметры.УстановитьЗначениеПараметра("Процесс", ФормаПроцесс);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    ОбновитьКоличествоШтрихкодов();

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    Элементы.СписокШтрихкодов.Шапка = Ложь;

#КонецЕсли

    Элементы.ПодсказкаПроцесс.Заголовок = Строка(ФормаПроцесс);
    Заголовок = СтрШаблон("Выполнение: %1", ФормаПроцесс);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

    Если ВводДоступен() И ИмяСобытия = ОповещенияФормыКлиент.ИмяСобытияСканированияШтрихкода() Тогда

        Штрихкод = Строка(Параметр);

        ОбработатьШтрихкод(Штрихкод);

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокШтрихкодов

// Процедура предлагает удалить штрихкод.
// 
// Параметры:
//  Элемент - ТаблицаФормы
//  Значение - Строка - Штрихкод
//  СтандартнаяОбработка - Булево
&НаКлиенте
Асинх Процедура УдалитьШтрихкодАсинх(Элемент, Значение, СтандартнаяОбработка)

    ОтветНаВопрос = Ждать ВопросАсинх("Удалить штрихкод?",
                                      РежимДиалогаВопрос.ДаНет,
                                      ,
                                      КодВозвратаДиалога.Нет); // КодВозвратаДиалога

    Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда

        ПоказатьОповещение("Удаление штрихкода");

        Результат = УдалитьШтрихкод(ФормаКомплектовщик, ФормаПроцесс, Значение);
        
        // Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
        Если Результат = Неопределено Тогда
            
            // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
            Возврат;

        КонецЕсли;

        ПоказатьОповещение("Штрихкод удален", Значение);

        ОбновитьФорму();

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ОбработатьШтрихкодСФормыАсинх(Команда)

    Значение = Ждать ВвестиСтрокуАсинх(ФормаШтрихкод, "Введите штрихкод");

    Если Не ЗначениеЗаполнено(Значение) Тогда

        Возврат;

    КонецЕсли;

    ФормаШтрихкод = Значение;
    ОбработатьШтрихкод(ФормаШтрихкод);

КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьШтрихкодыАсинх(Команда)

    КоличествоШтрихкодов = КоличествоШтрихкодов(ФормаКомплектовщик, ФормаПроцесс);
    
    // Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
    Если КоличествоШтрихкодов = Неопределено Тогда
        
        // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
        Возврат;

    КонецЕсли;

    Если КоличествоШтрихкодов = 0 Тогда

        ПоказатьОповещение("Штрихкоды отсутствуют");

        Возврат;

    КонецЕсли;

    ОтветНаВопрос = Ждать ВопросАсинх(СтрШаблон("Количество: %1", КоличествоШтрихкодов),
                                      РежимДиалогаВопрос.ДаНет,
                                      ,
                                      КодВозвратаДиалога.Нет,
                                      "Удалить все штрихкоды?"); // КодВозвратаДиалога

    Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда

        ПоказатьОповещение("Удаление штрихкодов");

        ШтрихкодыУдалены = УдалитьШтрихкодыКомплектовщика(ФормаКомплектовщик, ФормаПроцесс);
        
        // Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
        Если ШтрихкодыУдалены = Неопределено Тогда
            
            // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
            Возврат;

        КонецЕсли;

        ПоказатьОповещение("Удаление штрихкодов завершено");

    КонецЕсли;

    ОбновитьФорму();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеЗаданийДесмы

// Процедура обрабатывает штрихкод.
// 
// Параметры:
//  Штрихкод - Строка
&НаКлиенте
Процедура ОбработатьШтрихкод(Штрихкод)

    // 1. Определение типа штрихкода
    Тип = ШтрихкодыОбувиКлиентСервер.ТипШтрихкода(Штрихкод);

    // 2. Остановить обработку, если это не код маркировки
    Если Тип <> ПредопределенноеЗначение("Перечисление.ТипШтрихкода.КодМаркировки") Тогда

        ПоказатьВажноеОповещениеОбОшибке("Не является кодом маркировки", Штрихкод);

        Возврат;

    КонецЕсли;

    ПоказатьОповещение("Обработка кода маркировки", Штрихкод);

    ОписаниеОбработкиШтрихкода = ОбработатьШтрихкодКодаМаркировки(Штрихкод);
    
    // 2.5. Проверка, что вызов серверной функции прошел без потери соединения с основным сервером
    Если ОписаниеОбработкиШтрихкода = Неопределено Тогда
        
        // Из-за недоступности основного сервера должно появиться предложение перейти в автономный режим
        Возврат;

    КонецЕсли;
    
    // 3.1. Оповещение об успешной обработке
    Если ЭтоКодВозвратаУспех(ОписаниеОбработкиШтрихкода.КодВозврата) Тогда

        ПоказатьВажноеОповещениеОбУспешномВыполнении("Штрихкод обработан", Штрихкод);

        ОбновитьФорму();

    // 3.2. Оповещение о том, что штрихкод уже был обработан
    ИначеЕсли ЭтоКодВозвратаШтрихкодУжеОтсканирован(ОписаниеОбработкиШтрихкода.КодВозврата) Тогда

        ПояснениеОповещения = СтрШаблон("%1", ОписаниеОбработкиШтрихкода.ДатаПредыдущегоСканирования);

        ПоказатьВажноеОповещениеОбОшибкеДляКодаВозврата(ОписаниеОбработкиШтрихкода.КодВозврата,
                                                        "Штрихкод уже отсканирован",
                                                        ПояснениеОповещения);

    КонецЕсли;

КонецПроцедуры

// Процедура обрезает первые 31 символ кода маркировки и обрабатывает его.
// 
// Параметры:
//  Штрихкод - Строка
// 
// Возвращаемое значение:
//  См. ЗаданияДесмы.НовоеОписаниеОбработкиШтрихкода
&НаСервере
Функция ОбработатьШтрихкодКодаМаркировки(Знач Штрихкод)

    КМ31 = ШтрихкодыКлиентСервер.ЗаменитьСпециальныеСимволыКодаМаркировки(Лев(Штрихкод, 31), "#");

    Возврат ЗаданияДесмы.ОбработатьШтрихкод(ФормаКомплектовщик, ФормаПроцесс, КМ31);

КонецФункции

// См. ЗаданияДесмы.УдалитьШтрихкод
//  
// Параметры:
//  Комплектовщик - СправочникСсылка.Комплектовщики
//  Процесс - ПеречислениеСсылка.ПроцессНаДесме
//  Штрихкод - Строка
//  
// Возвращаемое значение:
//  Булево - Истина (или Неопределено, если оборвалось соединение с основным сервером)
&НаСервереБезКонтекста
Функция УдалитьШтрихкод(Знач Комплектовщик, Знач Процесс, Знач Штрихкод)

    ЗаданияДесмы.УдалитьШтрихкод(Комплектовщик, Процесс, Штрихкод);

    Возврат Истина;

КонецФункции

// См. ЗаданияДесмы.УдалитьШтрихкодыКомплектовщика
//  
// Параметры:
//  Комплектовщик - СправочникСсылка.Комплектовщики
//  Процесс - ПеречислениеСсылка.ПроцессНаДесме
//  
// Возвращаемое значение:
//  Булево - Истина (или Неопределено, если оборвалось соединение с основным сервером)
&НаСервереБезКонтекста
Функция УдалитьШтрихкодыКомплектовщика(Знач Комплектовщик, Знач Процесс)

    ЗаданияДесмы.УдалитьШтрихкодыКомплектовщика(Комплектовщик, Процесс);

    Возврат Истина;

КонецФункции

// См. ЗаданияДесмы.КоличествоШтрихкодовКомплектовщика
//@skip-check method-param-value-type
&НаСервереБезКонтекста
Функция КоличествоШтрихкодов(Знач Комплектовщик, Знач Процесс)

    //@skip-check invocation-parameter-type-intersect
    Возврат ЗаданияДесмы.КоличествоШтрихкодовКомплектовщика(Комплектовщик, Процесс);

КонецФункции

#КонецОбласти

#Область ОповещенияПользователя

// Функция возвращает ключ уникальности оповещений пользователя.
// 
// Возвращаемое значение:
//  Строка
&НаКлиенте
Функция КлючУникальностиОповещенийПользователя()

    Возврат ИмяФормы;

КонецФункции

// См. ОповещенияПользователяКлиент.ПоказатьОповещение
// 
// Параметры:
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьОповещение(Знач Заголовок, Знач Пояснение = "")

    ОповещенияПользователяКлиент.ПоказатьОповещение(Заголовок, Пояснение, КлючУникальностиОповещенийПользователя());

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал, соответствующий коду возврата и 
// показывает оповещение.
// 
// Параметры:
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьВажноеОповещениеОбУспешномВыполнении(Знач Заголовок, Знач Пояснение)

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеОбУспешномВыполнении();

#КонецЕсли

    ОповещенияПользователяКлиент.ПоказатьОповещение(Заголовок, Пояснение, КлючУникальностиОповещенийПользователя());

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал об ошибке и показывает важное оповещение об ошибке.
// 
// Параметры:
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьВажноеОповещениеОбОшибке(Знач Заголовок, Знач Пояснение)

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеОшибки();

#КонецЕсли

    ОповещенияПользователяКлиент.ПоказатьВажноеОповещениеОшибка(Заголовок,
                                                                Пояснение,
                                                                КлючУникальностиОповещенийПользователя());

КонецПроцедуры

// Процедура воспроизводит звуковой сигнал, соответствующий коду возврата и 
// показывает важное оповещение об ошибке.
// 
// Параметры:
//  КодВозврата - Число - См. КодыВозвратаСерверГлобальный
//  Заголовок - Строка
//  Пояснение - Строка
&НаКлиенте
Процедура ПоказатьВажноеОповещениеОбОшибкеДляКодаВозврата(Знач КодВозврата, Знач Заголовок, Знач Пояснение)

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

    СредстваМультимедиаКлиент.ВоспроизвестиЗвуковоеОповещениеДляКодаВозврата(КодВозврата);

#КонецЕсли

    ОповещенияПользователяКлиент.ПоказатьВажноеОповещениеОшибка(Заголовок,
                                                                Пояснение,
                                                                КлючУникальностиОповещенийПользователя());

КонецПроцедуры

#КонецОбласти

#Область Форма

// Процедура обновляет количество штрихкодов.
&НаКлиенте
Процедура ОбновитьКоличествоШтрихкодов()

    ФормаКоличествоШтрихкодов = КоличествоШтрихкодов(ФормаКомплектовщик, ФормаПроцесс);

КонецПроцедуры

// Процедура обновляет количество штрихкодов и список штрихкодов.
&НаКлиенте
Процедура ОбновитьФорму()

    ОбновитьКоличествоШтрихкодов();

    Элементы.СписокШтрихкодов.Обновить();

КонецПроцедуры

#КонецОбласти

#КонецОбласти